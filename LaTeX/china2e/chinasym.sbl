%% This file is part of the CHINASYM package, 
%% a PACKAGE for writing CHINESE CALENDAR SYMBOLS 
%% with LaTeX version 2e. 
%% 
%% -- Copyright 1997 by Udo Heyl, Eisenach. All rights reserved. -- 
%% 
%% Copying of this file is authorized only if either 
%% (1) you make absolutely no changes to your copy, including name, or 
%% (2) if you do make changes, you name it something other than 
%%          CHINASYM.ADD,     CHINASYM.ALF,     CHINASYM.ELE,
%%          CHINASYM.NUM,     CHINASYM.SBL,     CHINASYM.STA,
%%          CHINA10.MF   or   CHINA2E.STY !!!
%% This restriction helps ensure that all styles developed for 
%% DANTE e.V. (the users group of german speaking TeX users) 
%% remain identical. 
%% 
%% You are NOT ALLOWED to distribute this file alone. You are NOT 
%% ALLOWED to take money for the distribution or use of either this 
%% file or a changed version, except for some nominal charge for 
%% copying etc. 
%% 
%% Error Reports in case of UNCHANGED versions to 
%% 
%% Udo Heyl
%% Stregdaer Allee 7
%% 99817 Eisenach
%% Federal Republic of Germany 
%% 
%% or 
%% DANTE, Deutschsprachige Anwendervereinigung TeX e.V. 
%% Postfach 10 18 40 
%% 69008 Heidelberg 
%% Federal Republic of Germany 
%% Email: german@dante.de 
%% 

%% The symbolic letters

beginchar(192,U#,U#,0); "jun - Leap (Month, Year, Day)"; pickup mean_pen;
   z1=(a,0); z2=(a,7b); z3=(3.1a,8b); z4=(8a,8b); z5=(8a,b); z6=(7.5a,0);
   z7=(7a,.5b); z8=(1.5a,8.3b); z9=(2.1a,7.8b);
   z10=(2a,2b); z11=(7a,y10); z12=(x11-.4a,y11+.3b);
   z13=(2.5a,4b); z14=(6.5a,y13); z15=(x14-.4a,y14+.3b);
   z16=(2a,6b); z17=(7a,y16); z18=(x17-.4a,y17+.3b);
   z19=.5[z10,z11]; z20=.5[z16,z17]; draw z19--z20;
   draw z10--z11--z12; draw z13--z14--z15; draw z16--z17--z18;
   draw z1--z2; draw z8..{down}z9; draw z3--z4--z5{down}..{left}z6..z7;
endchar;

beginchar(187,.7U#,U#,0); "bold Complex"; pickup bold_pen;
   z1=(6a,7.5b); z2=(6a,5.5b); z3=(3.5a,7.5b); z4=(a,3.5b); z5=(3.5a,0); 
   z6=(6a,1.5b); z7=(6a,0); z8=(2.5a,7.2b); z9=(2.5a,.3b);
   draw z1{dir-120}..{dir-60}z2; draw z6{dir-120}..{dir-60}z7;
   draw z2..{left}z3..z8..{down}z4..z9..{right}z5..z6;
   pickup tiny_pen; draw z8--z9;
endchar;

beginchar(188,.7U#,U#,0); "bold Natural"; pickup bold_pen; 
   z1=(a,0); z2=(a,7.5b); z3=(6a,0); z4=(6a,7.5b); z5=(2.5a,5.5b); 
   z6=(2.5a,0); draw z1--z2--z3--z4; pickup tiny_pen; draw z5--z6--z1;
endchar;

beginchar(189,.7U#,U#,0); "bold Integer"; pickup bold_pen; 
   z1=(6a,3.75b); z2=(3.5a,7.5b); z3=(a,3.75b); z4=(3.5a,0); z5=(4a,1.6b);
   z6=(6a,0); z7=(2.5a,7.1b); z8=(2.5a,.55b);
   draw superellipse (z1,z2,z3,z4,.77); draw z5{right}..{right}z6; 
   pickup tiny_pen; draw z7--z8;
endchar;

beginchar(190,.8U#,U#,0); "bold Real"; pickup bold_pen; 
   z1=(a,0); z2=(a,3.75b); z3=(a,7.5b); z4=(3.5a,7.5b); z5=(6a,5.6b);
   z6=(3.5a,3.75b); z7=(7a,0); z8=(2.5a,0); z9=(2.5a,7.5b); z10=(3.5a,3.75b);
   draw z1--z3--z4{right}..z5..{left}z6--z2; draw z10{right}..{right}z7;
   pickup tiny_pen; draw z1--z8--z9;
endchar;

beginchar(191,.7U#,U#,0); "bold Integer"; pickup bold_pen;
   z1=(a,6.5b); z2=(a,7.5b); z3=(4.5a,7.5b); z4=(a,0); z5=(6a,0); 
   z6=(6a,b); z7=(2.5a,3.75b); z8=(5a,3.75b); z9=(2.5a,0); z10=(4.5a,7.5b);
   z11=(6a,7.5b);
   draw z1{dir60}..z2--z3; draw z10--z4--z5..{dir60}z6; draw z7--z8;
   pickup tiny_pen; draw z9--z11--z3;
endchar;

beginchar(193,.7U#,U#,0); "symbol A"; pickup syma_pen;
   z1=(a,0); z3=(2.75a,7.5b); z6=(4.25a,7.5b); z8=(6a,0); z4=(2.5a,0);
   z2=1/3[z1,z3]; z7=1/3[z8,z6]; draw z1--z3--z6--z8; draw z2--z7;
   pickup symb_pen; draw z1--z4--z6;
endchar;

beginchar(194,.7U#,U#,0); "symbol B"; pickup syma_pen; 
   z1=(a,0); z2=(a,4b); z3=(a,7.5b); z4=(3.5a,7.5b); z5=(5.5a,5.5b); 
   z6=(3.5a,4b); z10=(6a,2b); z11=(3.5a,0); z8=(2.5a,0); z9=(2.5a,7.5b); 
   draw z1--z3--z4..z5..z6--z2; draw z1--z11{right}..z10{up}..{left}z6;
   pickup symb_pen; draw z1--z8--z9;
endchar;

beginchar(195,.7U#,U#,0); "symbol C"; pickup syma_pen;
   z1=(6a,7.5b); z2=(6a,5.5b); z3=(3.5a,7.5b); z4=(a,3.5b); z5=(3.5a,0); 
   z6=(6a,1.5b); z7=(6a,0); z8=(2.5a,7.2b); z9=(2.5a,.3b);
   draw z1{dir-120}..{dir-60}z2; draw z6{dir-120}..{dir-60}z7;
   draw z2..{left}z3..z8..{down}z4..z9..{right}z5..z6;
   pickup symb_pen; draw z8--z9;
endchar;

beginchar(196,.7U#,U#,0); "symbol D"; pickup syma_pen;
   z1=(a,0); z2=(2.5a,0); z3=(6a,3.75b); z4=(2.5a,7.5b); z5=(a,7.5b);
   draw z1--z2..{up}z3..z4--z5; draw z1--z5; pickup symb_pen; draw z2--z4;
endchar;

beginchar(197,.7U#,U#,0); "symbol E"; pickup syma_pen;
   z1=(a,0); z2=(2.5a,0); z3=(6a,0); z4=(6a,b); z5=(a,3.75b); z6=(4.5a,3.75b); 
   z7=(a,7.5b); z8=(2.5a,7.5b); z9=(5.5a,7.5b); z10=(5.5a,6.5b);
   z11=(4.5a,3.25b); z12=(4.5a,4.25b);
   draw z10{dir120}..z9--z7--z1--z3..{dir60}z4; draw z5--z6; draw z11--z12;
   pickup symb_pen; draw z2--z8;
endchar;

beginchar(198,.6U#,U#,0); "symbol F"; pickup syma_pen;
   z1=(a,0); z2=(2.5a,0); z5=(a,3.75b); z6=(5a,3.75b); 
   z7=(a,7.5b); z8=(2.5a,7.5b); z9=(6a,7.5b); z10=(6a,6.5b);
   z11=(5a,3.25b); z12=(5a,4.25b);
   draw z10{dir120}..z9--z7--z1; draw z5--z6; draw z11--z12;
   pickup symb_pen; draw z1--z2--z8;
endchar;

beginchar(199,.7U#,U#,0); "symbol G"; pickup syma_pen;
   z1=(5.5a,7.5b); z2=(5.5a,6.5b); z3=(3.5a,7.5b); z4=(2.5a,7.2b);
   z5=(a,3.75b); z6=(2.5a,.3b); z7=(3.5a,0); z8=(5a,.3b); z9=(6a,3.75b);
   z10=(3.5a,3.75b);
   draw z1{dir-120}..{dir-60}z2{dir120}..{left}z3..z4..{down}z5%%
   ..z6..{right}z7..z8..{up}z9--z10; pickup symb_pen;
   draw z4--z6;
endchar;

beginchar(200,.7U#,U#,0); "symbol H"; pickup syma_pen;
   z1=(a,0); z2=(a,3.75b); z3=(a,7.5b); z4=(2.5a,0); z5=(2.5a,7.5b);
   z6=(6a,0); z7=(6a,3.75b); z8=(6a,7.5b); draw z1--z3; draw z6--z8; 
   draw z2--z7; pickup symb_pen; draw z1--z4--z5--z3;
endchar;

beginchar(201,.6U#,U#,0); "symbol I"; pickup syma_pen;
   z1=(a,0); z2=(2.5a,0); z3=(4a,0); z4=(5a,0); z5=(a,7.5b); z6=(2.5a,7.5b);
   z7=(4a,7.5b); z8=(5a,7.5b); draw z1--z4; draw z5--z8; draw z2--z6;
   pickup symb_pen; draw z3--z7;
endchar;

beginchar(202,.7U#,U#,0); "symbol J"; pickup syma_pen;
   z1=(a,7.5b); z2=(4.5a,7.5b); z3=(6a,7.5b); z4=(4.5a,2b); z5=(6a,2b);
   z6=(3a,0); z7=(a,1.5b); z8=(a,6.5b);
   draw z8{dir60}..z1--z2--z4{down}..{left}z6..{up}z7; 
   pickup symb_pen; draw z2--z3--z5{down}..{left}z6;
endchar;

beginchar(203,.8U#,U#,0); "symbol K"; pickup syma_pen;
   z1=(a,0); z2=(a,3.75b); z3=(a,7.5b); z4=(2.5a,0); z5=(2.5a,7.5b);
   z6=(3a,3.75b); z7=(6a,7.5b); z8=(7a,0);
   draw z1--z3; draw z2--z6{right}..{right}z8; draw z6{dir30}..z7;
   pickup symb_pen; draw z1--z4--z5--z3;
endchar;

beginchar(204,.7U#,U#,0); "symbol L"; pickup syma_pen;
   z1=(a,0); z2=(a,7.5b); z3=(2.5a,7.5b); z4=(2.5a,0); z5=(6a,0); z6=(6a,b);
   draw z2--z1--z5{dir120}..z6; pickup symb_pen; draw z2--z3--z4;
endchar;

beginchar(205,.9U#,U#,0); "symbol M"; pickup syma_pen;
   z1=(a,0); z2=(a,7.5b); z3=(2.5a,0); z5=(4.5a,3.75b); z4=3/7[z2,z5];
   z6=(8a,7.5b); z7=(8a,0); draw z1--z2--z5--z6--z7; pickup symb_pen;
   draw z1--z3--z4;
endchar;

beginchar(206,.7U#,U#,0); "symbol N"; pickup syma_pen; 
   z1=(a,0); z2=(a,7.5b); z3=(6a,0); z4=(6a,7.5b); z5=(2.5a,5.5b); 
   z6=(2.5a,0); draw z1--z2--z3--z4; pickup symb_pen; draw z5--z6--z1;
endchar;

beginchar(207,.7U#,U#,0); "symbol O"; pickup syma_pen; 
   z1=(6a,3.75b); z2=(3.5a,7.5b); z3=(a,3.75b); z4=(3.5a,0); z7=(2.5a,7b); 
   z8=(2.5a,.5b); draw superellipse (z1,z2,z3,z4,.77);
   pickup symb_pen; draw z7--z8;
endchar;

beginchar(208,.7U#,U#,0); "symbol P"; pickup syma_pen; 
   z1=(a,0); z2=(a,3.75b); z3=(a,7.5b); z4=(3a,7.5b); z5=(6a,5.6b); 
   z6=(3a,3.75b); z8=(2.5a,0); z9=(2.5a,7.5b); 
   draw z1--z3--z4{right}..z5..{left}z6--z2;
   pickup symb_pen; draw z1--z8--z9;
endchar;

beginchar(209,.7U#,U#,0); "symbol Q"; pickup syma_pen; 
   z1=(6a,3.75b); z2=(3.5a,7.5b); z3=(a,3.75b); z4=(3.5a,0); z5=(4a,1.6b);
   z6=(6a,0); z7=(2.5a,7.1b); z8=(2.5a,.55b);
   draw superellipse (z1,z2,z3,z4,.77); draw z5{right}..{right}z6; 
   pickup symb_pen; draw z7--z8;
endchar;

beginchar(210,.8U#,U#,0); "symbol R"; pickup syma_pen; 
   z1=(a,0); z2=(a,3.75b); z3=(a,7.5b); z4=(3.5a,7.5b); z5=(6a,5.6b);
   z6=(3.5a,3.75b); z7=(7a,0); z8=(2.5a,0); z9=(2.5a,7.5b); z10=(3.5a,3.75b);
   draw z1--z3--z4{right}..z5..{left}z6--z2; draw z10{right}..{right}z7;
   pickup symb_pen; draw z1--z8--z9;
endchar;

beginchar(211,.7U#,U#,0); "symbol S"; pickup syma_pen; 
   z1=(6a,7.5b); z2=(6a,6.5b); z3=(3.5a,7.5b); z4=(a,5.625b); z5=(3.5a,3.75b);
   z6=(6a,1.875b); z7=(3.5a,0); z8=(a,b); z9=(a,0); z10=(2.5a,7.3b);
   z11=(2.5a,3.95b); z12=(4.5a,3.55b); z13=(4.5a,.2b);
   draw z1{dir-120}..z2; draw z9{dir60}..z8; draw z2..{left}z3..z10%%
   ..{down}z4..z11..{right}z5..z12..{down}z6..z13..{left}z7..z8;
   pickup symb_pen; draw z10--z11; draw z12--z13;
endchar;

beginchar(212,.7U#,U#,0); "symbol T"; pickup syma_pen;
   z1=(a,6.5b); z2=(a,7.5b); z3=(2.75a,7.5b); z4=(4.25a,7.5b); z5=(6a,7.5b);
   z6=(6a,6.5b); z7=(2.75a,0); z8=(4.25a,0); draw z1..{dir120}z2--z5{dir-120}..z6; 
   draw z3--z7; pickup symb_pen; draw z7--z8--z4;
endchar;

beginchar(213,.7U#,U#,0); "symbol U"; pickup syma_pen;
   z1=(a,7.5b); z2=(a,3b); z3=(2.5a,.2b); z4=(3.5a,0); z5=(4.5a,.2b);
   z6=(6a,3b); z7=(6a,7.5b); z8=(2.5a,7.5b);
   draw z1--z2{down}..z3..{right}z4..z5..{up}z6--z7;
   pickup symb_pen; draw z1--z8--z3;
endchar;

beginchar(214,.7U#,U#,0); "symbol V"; pickup syma_pen;
   z1=(a,7.5b); z2=(2.5a,7.5b); z3=(6a,7.5b); z4=(2.75a,0); z5=(4.25a,0);
   draw z1--z4--z5--z3; pickup symb_pen; draw z1--z2--z5;
endchar;

beginchar(215,.8U#,U#,0); "symbol W"; pickup syma_pen;
   z1=(a,7.5b); z2=(2.5a,7.5b); z3=(7a,7.5b); z4=(4a,4b); 
   z5=(121/38a,45/19b); z6=(2a,0); z7=(6a,0); draw z1--z6--z4--z7--z3; 
   pickup symb_pen; draw z1--z2--z5;
endchar;

beginchar(216,.7U#,U#,0); "symbol X"; pickup syma_pen;
   z1=(a,7.5b); z2=(6a,0); z3=(6a,7.5b); z4=(a,0); 
   z5=(2.5a,0); z6=(6a,5.25b);
   draw z1--z2; draw z3--z4; pickup symb_pen; draw z4--z5--z6--z3;
endchar;

beginchar(217,.7U#,U#,0); "symbol Y"; pickup syma_pen;
   z1=(a,7.5b); z2=(6a,0); z3=( 6a,7.5b); z4=(a,0); 
   z5=(2.5a,0); z6=(6a,5.25b); z7=.5[z1,z2];
   draw z1--z7; draw z3--z4; pickup symb_pen; draw z3--z6--z5--z4;
endchar;

beginchar(218,.7U#,U#,0); "symbol Z"; pickup syma_pen;
   z1=(a,6.5b); z2=(a,7.5b); z3=(4.5a,7.5b); z4=(a,0); z5=(6a,0); 
   z6=(6a,b); z7=(2.5a,3.75b); z8=(5a,3.75b); z9=(2.5a,0); z10=(4.5a,7.5b);
   z11=(6a,7.5b);
   draw z1{dir60}..z2--z3; draw z10--z4--z5..{dir60}z6; draw z7--z8;
   pickup symb_pen; draw z9--z11--z3;
endchar;

beginchar(219,.7U#,U#,0); "symbol AE"; pickup syma_pen;
   z1=(a,0); z3=(2.75a,6b); z6=(4.25a,6b); z8=(6a,0); z4=(2.5a,0);
   z2=1/3[z1,z3]; z7=1/3[z8,z6];
   draw z1--z3--z6--z8; draw z2--z7;
   pickup symb_pen; draw z1--z4--z6;
   z20=(2.5a,7b); z21=(4.5a,7b);
   fill fullcircle scaled 1.5pt shifted z20;
   unfill fullcircle scaled .7pt shifted z20;
   fill fullcircle scaled 1.5pt shifted z21;
   unfill fullcircle scaled .7pt shifted z21;
endchar;

beginchar(220,.7U#,U#,0); "symbol OE"; pickup syma_pen; 
   z1=(6a,3b); z2=(3.5a,6b); z3=(a,3b); z4=(3.5a,0); z7=(2.5a,5.6b); 
   z8=(2.5a,.4b); draw superellipse (z1,z2,z3,z4,.77);
   pickup symb_pen; draw z7--z8;
   z20=(2.5a,7b); z21=(4.5a,7b);
   fill fullcircle scaled 1.5pt shifted z20;
   unfill fullcircle scaled .7pt shifted z20;
   fill fullcircle scaled 1.5pt shifted z21;
   unfill fullcircle scaled .7pt shifted z21;
endchar;

beginchar(221,.7U#,U#,0); "symbol UE"; pickup syma_pen;
   z1=(a,6b); z2=(a,2.4b); z3=(2.5a,.16b); z4=(3.5a,0); z5=(4.5a,.16b);
   z6=(6a,2.4b); z7=(6a,6b); z8=(2.5a,6b);
   draw z1--z2{down}..z3..{right}z4..z5..{up}z6--z7;
   pickup symb_pen; draw z1--z8--z3;
   z20=(2.5a,7b); z21=(4.5a,7b);
   fill fullcircle scaled 1.5pt shifted z20;
   unfill fullcircle scaled .7pt shifted z20;
   fill fullcircle scaled 1.5pt shifted z21;
   unfill fullcircle scaled .7pt shifted z21;
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% END OF CHINASYM.SBL %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
