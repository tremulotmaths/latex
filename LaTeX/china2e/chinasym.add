%% This file is part of the CHINASYM package, 
%% a PACKAGE for writing CHINESE CALENDAR SYMBOLS 
%% with LaTeX version 2e. 
%% 
%% -- Copyright 1997 by Udo Heyl, Eisenach. All rights reserved. -- 
%% 
%% Copying of this file is authorized only if either 
%% (1) you make absolutely no changes to your copy, including name, or 
%% (2) if you do make changes, you name it something other than 
%%          CHINASYM.ADD,     CHINASYM.ALF,     CHINASYM.ELE,
%%          CHINASYM.NUM,     CHINASYM.SBL,     CHINASYM.STA,
%%          CHINA10.MF   or   CHINA2E.STY !!!
%% This restriction helps ensure that all styles developed for 
%% DANTE e.V. (the users group of german speaking TeX users) 
%% remain identical. 
%% 
%% You are NOT ALLOWED to distribute this file alone. You are NOT 
%% ALLOWED to take money for the distribution or use of either this 
%% file or a changed version, except for some nominal charge for 
%% copying etc. 
%% 
%% Error Reports in case of UNCHANGED versions to 
%% 
%% Udo Heyl
%% Stregdaer Allee 7
%% 99817 Eisenach
%% Federal Republic of Germany 
%% 
%% or 
%% DANTE, Deutschsprachige Anwendervereinigung TeX e.V. 
%% Postfach 10 18 40 
%% 69008 Heidelberg 
%% Federal Republic of Germany 
%% Email: german@dante.de 
%% 

%%% This File contains ADDITIONAL SYMBOLS %%

%% The Euro Symbol %%
beginchar(255,U#,U#,0); "euro"; pickup thick_pen; a=.1w; b=.1h; 
   z1=(8a,6b); z2=(8a,1.5b); z3=(5.5a,7.5b); z4=(x3,0); z5=(3a,y1);
   z6=(x5,y2); z7=1/3[z1,z6]; z8=2/3[z1,z6]; x9=x10=a; y9=y7; y10=y8;
   draw z1..z3..z5..z6..z4..z2; draw z9--z7; draw z10--z8;
endchar; 

%% Moon Phases %%

beginchar(123,U#,U#,0); "The New Moon"; pickup tiny_pen; 
   z1=(4.5a,4b); z2=(3a,5b); z3=(6a,5b);
   z4=(3.5a,3.5b); z5=(5.5a,3.5b); z6=(2.5a,2b); z7=(6.5a,2b);
   fill fullcircle scaled 8pt shifted z1;
   unfill fullcircle scaled 1.3pt shifted z2;
   unfill fullcircle scaled 1.3pt shifted z3;
   unfill z4{dir30}..{dir-30}z5{dir210}..{dir150}z4..cycle;
   unfill z6{dir-10}..{dir10}z7{dir230}..{dir130}z6..cycle;
endchar;

beginchar(124,U#,U#,0); "The first Quarter"; pickup tiny_pen;
   z1=(2.5a,0); z2=(6.5a,4b); z3=(2.5a,8b); z4=(3a,5b); z5=(a,4b);
   z6=(3a,3.5b); z7=(2.5a,2.7b); z8=(5a,3b); z9=(3a,1.8b); z10=(4.5a,5b);
   fill z1{right}..{up}z2..{left}z3{dir-60}..z4--z5--z6--z7--z8--z9%%%
   ..{dir-120}z1..cycle; unfill fullcircle scaled 1.3pt shifted z10;
endchar;

beginchar(125,U#,U#,0); "The Full Moon"; pickup thick_pen; 
   z1=(4.5a,4b); z2=(3a,5b); z3=(6a,5b); 
   z4=(4a,3.5b); z5=(5a,3.5b); z6=(3a,2b); z7=(6a,2b);    
   draw fullcircle scaled 8pt shifted z1; pickup thin_pen;
   draw fullcircle scaled 1pt shifted z2;
   draw fullcircle scaled 1pt shifted z3;
   draw z4{dir30}..{dir-30}z5{dir210}..{dir150}z4..cycle;
   draw z6{dir-10}..{dir10}z7{dir220}..{dir140}z6..cycle;
endchar;

beginchar(126,U#,U#,0); "The third Quarter"; pickup thick_pen;
   z1=(6.5a,0); z2=(2.5a,4b); z3=(6.5a,8b); z4=(6a,5b); z5=(8a,4b);
   z6=(6a,3.5b); z7=(6.5a,2.5b); z8=(4a,3b); z9=(6a,2b); z10=(4.5a,5b);
   draw z9..{dir-60}z1{left}..{up}z2..{right}z3{dir-120}..z4--z5--z6--z7;
   pickup thin_pen; draw fullcircle scaled 1pt shifted z10; draw z7--z8--z9;
endchar;

%% Der gruene Punkt %%

beginchar(127,U#,U#,0); "DER GRUENE PUNKT"; pickup tiny_pen;
   z1=(5a,8b); z2=(5a,7.5b); z3=(5a,6.5b); z4=(5a,4.5b); z5=(5a,3.5b); 
   z6=(5a,1.5b); z7=(5a,.5b); z8=(5a,0); z9=(2.8a,2.5b); z10=(7.2a,5.5b);
   z11=(a,4b); z12=(9a,4b); z13=(2.17a,6.83b); z14=(2.17a,1.17b);
   z15=(7.83a,1.17b); z16=(7.83a,6.83b); z17=(2.7a,5.55b); z18=(2.7a,.95b);
   z19=(7.3a,2.45b); z20=(7.3a,7.05b); z21=(1.75a,3.25b); z22=(8.25a,4.75b);
   fill z1{right}..z20{dir-45}..z22{down}..z19{dir-135}..{left}z6%%
   --z7{dir160}..{dir110}z9{dir70}..{dir20}z4--z5{dir20}..{dir70}%%
   z10{dir110}..{dir160}z2--z3{left}..z17{dir-135}..z21{down}%%
   ..z18{dir-45}..z8{right}..z15{dir45}..z12{up}..z16{dir135}%%
   ..{left}z1..cycle; 
   draw fullcircle scaled 7pt shifted 1/2[z1,z8];
endchar;

%% Additional Chinese Calendarical and Chronological Symbols %%

beginchar(161,U#,U#,0); "hua - the Flower"; pickup mean_pen;   
%% part of the Cycle of 60 years ** hua-kia-tse ** %%
   z1=(a,b); z2=(4a,5b); z3=(3.5a,3b); z4=(3.5a,0); z5=(5a,5b); z6=(4.9a,3b);
   z7=(5a,1.3b); z8=(8a,1.3b); z9=(7.2a,2b); z10=(4.1a,2.4b);
   z11=(6.1a,5b); z12=(2.5a,6.5b); z13=(7a,6.5b); z14=(4a,6b);
   z15=(5.5a,6b); z16=(3.5a,7.4b); z17=(6a,8b); z18=(6a,b);
   draw z1--z2; draw z3--z4; 
   draw z5..z6{z7-z5}..z7{z8-z5}..{z8-z18}z18--z8;
   draw z8..{up}z9; draw z12--z13; draw z14{up}..z16;
   draw z15..{up}z17; draw z10..{up}z11;
endchar;

beginchar(162,U#,U#,0); "nien - the year"; pickup thick_pen;
   z1=(a,2.5b); z2=(3.5a,2.5b); z3=(8a,2.5b); z4=(7.6a,2.8b);
   z5=(3a,4.5b); z6=(3.5a,4.5b); z7=(7a,4.5b); z8=(6.6a,4.8b);
   z9=(3.2a,6.5b); z10=(7.5a,6.5b); z11=(7.1a,6.8b); z12=(5a,6.5b);
   z13=(5a,0); z14=(a,3.5b); z15=(3.5a,8b);
   draw z1--z3--z4; draw z5--z7--z8; draw z9--z10--z11; draw z2--z6;
   draw z12--z13; draw z14{dir45}..z9{z15-z14}..{up}z15;
endchar;

beginchar(163,U#,U#,0); "jih - the day"; pickup thick_pen;
   for i=7,8,9,10,11 : x[i]=a; x[i+5]=8a; endfor;
   y7=y12=0; y11=y16=8b; y8=y13=.5b; y10=y15=7.5b; y9=y14=1/2[y8,y10];
   draw z7--z11; draw z12--z16; draw z8--z13; draw z9--z14; draw z10--z15;
endchar;

%% Chinese Numeric Symbols %%

beginchar(164,U#,U#,0); "tsien = 1000"; pickup thick_pen;
   z1=(4.5a,0); z2=(a,3.5b); z3=(8a,3.5b); z4=(7.6a,3.8b);
   z5=(2a,6.5b); z7=(7a,8b); z6=(1/2[x5,x7],1/5[y5,y7]);
   draw z1--z6; draw z2--z3--z4; draw z5..z6{z7-z5}..z7;
endchar;

beginchar(165,U#,U#,0); "pai = 100"; pickup thick_pen;
   z1=(2a,0); z2=(2a,5.9b); z3=(7.2a,6.3b); z4=(7a,5.9b); z5=(7a,0); 
   z6=(6.3a,.4b); z7=(2a,b); z8=(7a,b); z9=1/2[z2,z7]; z10=1/2[z4,z8];
   z11=(a,8b); z12=(8a,8b); z13=(3.5a,6.3b); z14=(4.5a,7.8b);
   draw z1--z2--z4--z5--z6; draw z7--z8; draw z9--z10; draw z4{up}..z3;
   draw z11--z12; draw z13{up}..z14;
endchar;

beginchar(166,U#,U#,0); "ling = 0"; pickup thick_pen;
   z1=(a,2b); z2=(4.5a,5b); z3=(8a,2b); z4=(4.5a,8b); z5=(2.5a,8b);
   z6=(7a,8b); z7=(6.6a,8.3b); z8=(a,5.5b); z9=(2a,7b); z10=(8a,7b);
   z11=(7.5a,6b); z12=(2.8a,6b); z15=(6.2a,6b); z16=(2.8a,5b); z19=(6.2a,5b);
   z13=1/4[z12,z15]; z14=3/4[z12,z15]; z17=1/4[z16,z19]; z18=3/4[z16,z19];
   z20=(4a,3.1b); z21=(4.6a,2.5b); z22=(2.5a,2b); z23=(6a,2b);
   z25=(3.5a,b); z26=(5.6a,0); z24=(1/2[x25,x26],1/2[y25,y26]+.3b);
   draw z1{dir20}..{dir60}z2{dir-60}..{dir-20}z3; draw z2--z4;
   draw z5--z6--z7; draw z8{dir30}..{up}z9--z10{dir-150}..{down}z11;
   draw z12--z13; draw z14--z15; draw z16--z17; draw z18--z19;
   draw z20..{down}z21; draw z22--z23; draw z24{dir80}..{dir30}z23;
   draw z25{dir-10}..{dir-50}z26;
endchar;

beginchar(167,U#,U#,0); "xin - NEW"; pickup mean_pen;
%% part of NEW Year, NEW Moon etc. pp. %%
   z1=(a,7b); z2=(4.5a,7b); z3=(4.1a,7.3b); z4=(a,5b); z5=(4.5a,5b);
   z6=(4.1a,5.3b); z7=(1.5a,3b); z8=(4a,3b); z9=(3.6a,3.3b); z10=(a,0);
   z11=(2a,2.5b); z12=(3.5a,2.5b); z13=(4a,b); z14=(2.75a,5b); z15=(4a,6.7b);
   z17=(1.5a,6.5b); z16=.9[z17,z14]; z18=.65[z1,z3]; z19=(2a,8b);
   z20=(5.5a,7.5b); z21=(5.5a,7b); z22=(7.5a,8b); z23=(5.5a,4.5b); 
   z24=(7a,4.5b); z25=(8a,4.5b); z26=(7.6a,4.8b); z27=(7a,0); z28=(4a,0); 
   z29=(2.75a,0); z30=(2.3a,.5b); z31=(5.5a,3b);
   draw z30--z29--z14--z15; draw z1--z2--z3; draw z4--z5--z6;
   draw z7--z8--z9; draw z24--z27; draw z20--z31{down}..z28; 
   draw z23--z25--z26; draw z10{dir80}..z11; draw z12{dir-30}..z13; 
   draw z14--z15; draw z16{dir110}..z17; draw z18{dir110}..z19; 
   pickup thick_pen; draw z21--z22;
endchar;

beginchar(168,U#,U#,0); "shu - book"; pickup thick_pen;
%% part of QIAN NIAN SHU - the Book of thousand Years (Chinese Calendar) **
   z1=(4.5a,0); z2=(4.5a,8b); x3=x4=x1; y3=1/3[y1,y2]; y4=2/3[y1,y2];
   y7=y3=y8=y9; y5=y4=y6; x6=x8=6.3a; x5=2.5a; x7=a; x9=8a; z10=(6a,b);
   z11=(6a,7b); z12=(8a,5b); z13=(8a,2b); draw z11{dir-10}..{dir-80}z12;
   draw z1--z2; draw z7--z9; draw z5--z6{dir-120}..z8; 
   draw z9--z13..{dir130}z10;
endchar;

beginchar(169,U#,U#,0); "siu - constellation"; pickup thick_pen;
   z1=(1.5a,5b); z2=(3a,6.3b); z3=1/2[z1,z2]; z4=(x3,0); z5=(a,y2);
   z6=(x1,7b); z7=(8a,y6); z8=(7.6a,7.3b); z9=(3.5a,8b); z10=(5a,y8);
   z11=(x9,y3); z12=(x7,y3); z13=(x8,y3+.3b); x16=x18=x20=4a; x15=x10;
   x17=x19=x21=7.5a; y16=y17=y15=4.5b; y20=y21=0; y18=y19=2.25b;
   z14=2/3[z11,z12]; draw z1--z2; draw z3--z4; draw z5..{up}z6--z7--z8; 
   draw z9{right}..{dir-60}z10; draw z11--z12--z13; draw z14{left}..z15; 
   draw z18--z19--z17--z16--z20--z21--z19;
endchar;

beginchar(170,U#,U#,0); "tsie - the 1st Half Month"; pickup mean_pen;
   for i=1,2,3,4,5,6 : x[i]=1.5a; endfor; x7=a; x8=2.5a; x9=x10=x11=4.5a;
   x12=x17=x16=3a; x13=x14=4a; x15=x19=x18=3.6a; x20=x8-.3a; y1=y21=0; 
   y2=y13=y30=2b; y3=y12=y22=3b; y4=y11=4b; y5=y10=5b; 
   y6=y17=y9=y23=y24=y25=6b; y7=y6-.3b; y20=y16=y14=y26=y28=y29=7b; 
   y8=y27=8b; y18=1/2[y14,y17]; y15=y14+.3b; y19=y13+.3b; 
   for i=21,22,23 : x[i]=5.5a; endfor; x24=6.5a; x26=x27=1/2[x23,x24]; 
   x29=7.6a; x25=x28=x30=8a;
   draw z1--z6; draw z7..z6..z20..z8; draw z20--z14--z15; draw z16--z18--z17;
   draw z6--z9--z11--z4; draw z5--z10; draw z2--z13--z19; draw z3--z12;
   draw z21--z23--z25--z30{dir120}..{left}z22; draw z23..{up}z26--z28--z29;
   draw z27--z26..{down}z24;
endchar;

beginchar(171,U#,U#,0); "ki - the 2nd Half Month"; pickup mean_pen;
   z1=(a,0); x2=x8=3.2a; x3=x5=4.5a; y2=y3=3b; y5=y8=4b; z7=(2a,5b);
   z4=(6a,0); z6=(x4,y7); z9=(a,3.5b); z10=(6a,3.5b); z11=(4a,0); 
   z12=(4a,5b); z13=(2a,5.5b); z14=(7a,y13); z15=(8a,0); z16=(8a,b); 
   z17=(a,y13); z18=(2.5a,8b); z19=.44[z17,z18]; z20=.88[z17,z18]; 
   z21=(x14,y19); z22=(x15,y20); z23=(x21-.4b,y21+.3b); z24=(x22-.4b,y22+.3b);
   draw z9--z10; draw z11--z12; draw z1{dir20}..z2; draw z4..{up}z3;
   draw z8..{left}z7; draw z5..{right}z6; draw z13--z14{dir-100}..{right}%%
   z15{dir120}..z16; draw z19--z21--z23; draw z20--z22--z24; draw z17--z18;
endchar;

beginchar(172,U#,U#,0); "li - the calendar"; pickup mean_pen;
   z1=(a,0); z2=(2.5a,8b); z3=(8a,8b); z4=(7.6a,8.3b); z5=(5.5a,7b);
   z6=(5.3a,4.5b); z7=(2a,0); z8=(3.5a,4.5b); z9=(8a,4.5b);
   z11=(5a,b); z12=(6a,0);
   draw z1{dir50}..{up}z2--z3--z4; draw z5..z6..{dir200}z7; 
   draw z8-- z9{dir-100}..{left}z12..{dir120}z11;
endchar;

beginchar(173,U#,U#,0); "yang - male / positive / solar"; pickup mean_pen;
   z1=(a,0); z2=(a,8b); z3=(3a,8b); z4=(2.5a,5.5b); z5=(3a,3.5b);
   z6=(2a,2b); draw z1--z2{dir-30}..{dir30}z3; draw z3..z4..z5..{left}z6;
   for i=7,8,9,10,11 : x[i]=4.5a; x[i+5]=8a; endfor;
   y7=y12=0; y11=y16=8b; y8=y13=.5b; y10=y15=7.5b; y9=y14=1/2[y8,y10];
   draw z7--z11; draw z12--z16; draw z8--z13; draw z9--z14; draw z10--z15;
endchar;

beginchar(174,U#,U#,0); "yin - female / negative / lunar"; pickup mean_pen;
   z1=(a,0); z2=(a,8b); z3=(3a,8b); z4=(2.5a,5.5b); z5=(3a,3.5b);
   z6=(2a,2b); draw z1--z2{dir-30}..{dir30}z3; draw z3..z4..z5..{left}z6;
   for i=7,8,9,10,11 : x[i]=4.5a; x[i+5]=8a; endfor; 
   z17=(8a,1.3b); z18=(6.5a,.3b); z19=(3.5a,0);
   y7=y12=0; y11=y16=8b; y8=y13=3b; y10=y15=7.5b; y9=y14=1/2[y8,y10];
   draw z19..{up}z8--z11; draw z8--z13; draw z9--z14; draw z10--z15;
   draw z16--z14{down}..z17{down}..{dir150}z18;
endchar;

beginchar(175,U#,U#,0); "jie - moment"; pickup thick_pen;
   z1=(4a,0); z2=(1.5a,4.5b); z14=(x1,y2); z3=(7.5a,y2); z4=(x3,2b);
   z5=(6.5a,b); z6=(5.5a,1.5b); z7=(a,6.5b); z8=(8a,y7); z9=(7.6a,6.5b);
   z10=(3a,5.5b); z11=(x10+.5a,8b); z12=(6a,y10); z13=(x12+.5a,y11);
   draw z2--z3--z4{down}..{left}z5..z6; draw z1--z14;
   draw z7--z8--z9; draw z10--z11; draw z12--z13;
endchar;

beginchar(176,U#,U#,0); "yuan - beginning"; pickup thick_pen;
   z1=(a,6b); z2=(8a,y1); z3=(7.6a,y1+.3b); z4=(2a,8b); z5=(7a,8b);
   z6=(6.6a,8.3b); z7=(a,0); z8=(3a,3b); z9=(3a,y1); z10=(5a,y1);
   z11=(5a,b); z12=(6a,0); z13=(8a,0); z14=(8a,1.5b);
   draw z1--z2--z3; draw z4--z5--z6; draw z7..{up}z8--z9;
   draw z10--z11{down}..{right}z12--z13{dir120}..{dir60}z14;
endchar;

beginchar(177,U#,U#,0); "dan - the morning"; pickup thick_pen;
   z1=(a,0); z2=(8a,0); z3=(7.6a,.3b); draw z1--z2--z3;
   for i=7,8,9,10,11 : x[i]=2a; x[i+5]=7a; endfor;
   y7=y12=2b; y11=y16=8b; y8=y13=2.3b; y10=y15=7.7b; y9=y14=1/2[y8,y10];
   draw z7--z11; draw z12--z16; draw z8--z13; draw z9--z14; draw z10--z15;
endchar;

beginchar(178,U#,U#,0); "di - earthly"; pickup mean_pen;
   z1=(a,0); z3=(3a,b); z2=1/2[z1,z3]; z5=(x2,7.5b); z4=(x1,6b);
   z6=(x3,y4); z7=(7a,6b); x8=x3; (y7-y8)*(x3-x1)=(y3-y1)*(x7-x8);
   z9=(4a,8b); z10=(x9,2b); z11=(6a,0); z12=(8a,0); z13=(8a,1.5b);
   z14=(4/7[x8,x7],7b); z15=(x14,2b); z16=(x7,2.5b); z17=(x7-.3a,2b);
   z18=(x7-.6a,2.2b); draw z4--z6; draw z2--z5; draw z8--z7;
   draw z9--z10{down}..{right}z11--z12{dir120}..z13; draw z14--z15;
   draw z7--z16{down}..{left}z17..z18; pickup thick_pen; draw z1--z3;
endchar;

beginchar(179,U#,U#,0); "zhi - branch"; pickup thick_pen;
   z1=(a,0); for i=2,3,4: z[i]=((12-2.5*i)*a,4b); endfor; z5=(8a,0);
   z6=(x3,8b); z7=(a,6b); z8=(8a,y7); z9=(x8-.4a,y8+.3a);
   draw z1{right}..{dir60}z2--z4{dir-60}..{right}z5;
   draw z3--z6; draw z7--z8--z9;
endchar;

beginchar(180,U#,U#,0); "tian - heaven"; pickup thick_pen;
   z1=(a,0); z2=(8a,0); z3=(4.5a,8b); z4=(a,4.5b); z5=(8a,y4);
   z6=(x5-.4a,y5+.3b); z7=(2a,8b); z8=(7a,y7); z9=(x8-.4a,y8+.3b);
   draw z1{right}..{up}z3{down}..{right}z2; draw z4--z5--z6;
   draw z7--z8--z9;
endchar;

beginchar(181,U#,U#,0); "gan - branch"; pickup thick_pen;
   z1=(4.3a,0.4b); z2=(4.5a,0); z3=(4.5a,8b); z4=(a,4.5b); z5=(8a,y4);
   z6=(x5-.4a,y5+.3b); z7=(2a,8b); z8=(7a,y7); z9=(x8-.4a,y8+.3b);
   draw z1--z2--z3; draw z4--z5--z6; draw z7--z8--z9;
endchar;

beginchar(182,U#,U#,0); "mu - WOOD"; pickup thick_pen;
   z1=(4.5a,0); z2=(x1,5b); z3=(x1,8b); z4=(a,y2); z5=(8a,y2);
   z6=(x4,2b); z7=(x5,y6); draw z1--z3; draw z4--z5;
   draw z6{dir20}..z2; draw z7{dir160}..z2;
endchar;

beginchar(183,U#,U#,0); "huo - FIRE"; pickup thick_pen;
   z1=(a,0); z2=(4.5a,8b); z3=(8a,0); z4=(5a,4b); z5=(7a,7b); z6=(2a,4b);
   z7=(3a,6b); draw z1{dir20}..{up}z2{down}..{dir-20}z3; draw z4--z5;
   draw z6..{up}z7;
endchar;

beginchar(184,U#,U#,0); "tu - EARTH"; pickup thick_pen;
   z1=(4.3a,7.6b); z2=(4.5a,8b); z3=(4.5a,0); z4=(a,0); z5=(8a,y4);
   z6=(x5-.4a,y5+.3b); z7=(2a,4b); z8=(7a,y7); z9=(x8-.4a,y8+.3b);
   draw z1--z2--z3; draw z4--z5--z6; draw z7--z8--z9;
endchar;

beginchar(185,U#,U#,0); "jin - METAL"; pickup thick_pen;
   z1=(a,0); z2=(8a,y1); z3=(x2-.4a,y2+.3b); z11=1/2[z1,z2];  
   z4=(2a,3b); z5=(7a,y4); z6=(x5-.4a,y5+.3b); 
   z7=(3a,4.5b); z8=(6a,y7); z9=(x8-.4a,y8+.3b); z10=1/2[z7,z8];
   z12=(5a,0); z13=(7a,2.2b); z14=(2a,2b); z15=(3.5a,.8b);
   z16=(a,4b); z17=(4.5a,8b); z18=(8a,y16);
   draw z1--z2--z3; draw z4--z5--z6; draw z7--z8--z9;
   draw z14{dir-20}..z15; draw z12{dir60}..z13; draw z11--z10;
   draw z16..{dir60}z17{dir-60}..z18;
endchar;

beginchar(186,U#,U#,0); "shui - WATER"; pickup thick_pen;
   z1=(4.5a,8b); z2=(x1,b); z3=(4.2a,0); z4=(3.9a,.5b); z5=(a,2b);
   z6=(3.5a,5.5b); z7=(1.5a,5b); z8=(7a,6.5b); z9=(5a,5.6b); z10=(8a,2b);
   draw z1--z2{down}..{left}z3..z4; draw z5{dir30}..{up}z6; 
   draw z6..{left}z7; draw z8..{left}z9; draw z9..{dir-30}z10;
endchar;
