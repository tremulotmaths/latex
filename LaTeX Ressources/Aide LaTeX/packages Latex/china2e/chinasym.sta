%% This file is part of the CHINASYM package, 
%% a PACKAGE for writing CHINESE CALENDAR SYMBOLS 
%% with LaTeX version 2e. 
%% 
%% -- Copyright 1997 by Udo Heyl, Eisenach. All rights reserved. -- 
%% 
%% Copying of this file is authorized only if either 
%% (1) you make absolutely no changes to your copy, including name, or 
%% (2) if you do make changes, you name it something other than 
%%          CHINASYM.ADD,     CHINASYM.ALF,     CHINASYM.ELE,
%%          CHINASYM.NUM,     CHINASYM.SBL,     CHINASYM.STA,
%%          CHINA10.MF   or   CHINA2E.STY !!!
%% This restriction helps ensure that all styles developed for 
%% DANTE e.V. (the users group of german speaking TeX users) 
%% remain identical. 
%% 
%% You are NOT ALLOWED to distribute this file alone. You are NOT 
%% ALLOWED to take money for the distribution or use of either this 
%% file or a changed version, except for some nominal charge for 
%% copying etc. 
%% 
%% Error Reports in case of UNCHANGED versions to 
%% 
%% Udo Heyl
%% Stregdaer Allee 7
%% 99817 Eisenach
%% Federal Republic of Germany 
%% 
%% or 
%% DANTE, Deutschsprachige Anwendervereinigung TeX e.V. 
%% Postfach 10 18 40 
%% 69008 Heidelberg 
%% Federal Republic of Germany 
%% Email: german@dante.de 
%% 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% This file contains the moon-symbols and the so called %%
%% STATIONs of the Moon (or Moon-Houses),  to be used in %%
%% China for the names of the days of the 28-days-week!! %%
%% written by Udo Heyl, Eisenach (Version 1.0) 97/05/15. %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar(95+o,U#,U#,0); "wei - the ridge"; pickup thick_pen;
   z1=(a,0); z2=(2.5a,2b); z3=(2.5a,5b); z4=(a,4.5b); z5=(1.7a,5b);
   z6=(3.5a,7b); z7=(4.5a,8b); z8=(5a,4.5b); z9=(5.5a,7b); z10=(6.5a,8b);
   z11=(8a,5b); z12=(7.6a,5.3b); z13=(6a,3b); z14=(5a,4b); z15=(4a,3b);
   z16=(4a,1.5b); z17=(5.5a,0); z18=(8a,0); z19=(8a,2b);
   draw z1{dir45}..{up}z2--z3; draw z4--z7; draw z5--z11--z12;
   draw z6--z9; draw z8{up}..{dir45}z10; draw z13{up}..{left}%%%
   z14..{down}z15--z16{down}..{right}z17--z18{dir130}..z19;
endchar;

beginchar(96+o,U#,U#,0); "shi - the place of sacrifice"; pickup thick_pen;    
   z1=(a,0); z2=(8a,0); z3=(1.5a,1.5b); z4=(7.5a,1.5b); z5=(2a,3b);
   z6=(5.5a,3.75b); z7=(7.2a,2.25b); z8=(7a,3b); z9=(4.5a,3b); z10=(4.5a,0);
   z11=(2.5a,4.5b); z12=(6.5a,4.5b); z13=(6.1a,4.8b); z14=(a,5b);
   z15=(2a,7b); z16=(8a,5b); z17=(7a,7b); z18=(1.5a,6b); z19=(7.5a,6b);
   z20=(4.5a,6b); z21=(4.5a,8b); draw z9--z10; draw z11--z12--z13;
   draw z14--z15; draw z16--z17; draw z18--z19; draw z20--z21;
   draw z1--z2; draw z3--z4; draw z8--z5{right}..{right}z6..{down}z7;
endchar;

beginchar(97+o,U#,U#,0); "pi - the wall"; pickup mean_pen;
   z1=(a,0); z2=(4.5a,0); z3=(8a,0); z4=(7.6a,.3b); z5=(1.5a,1.5b);
   z7=(7.5a,1.5b); z8=(7.1a,1.8b); z9=(2a,2.5b);
   z10=(2a,3b); z11=(4a,3b); z12=(4.5a,3b); z14=(6.2a,2.3b);
   z15=(8a,3b); z16=(7.6a,3.3b); z17=(4a,4.5b); z18=(2a,4.5b);
   z19=(a,3.5b); z20=(2a,6.5b); z21=(4a,6.5b); z22=(4a,8b);
   z23=(2a,8b); z25=(5.2a,4.5b); z26=(5.6a,4.5b); z27=(6.2a,4.5b);
   z28=(8a,4.5b); z29=(7.6a,4.8b); z30=(5a,7b); z31=(6.5a,7b);
   z32=(8a,7b); z33=(6.5a,8b); z34=(7.5a,7b);
   draw z1--z3--z4; draw z5--z7--z8; draw z10--z15--z16; draw z2--z12;
   draw z9--z18--z10--z15--z16; draw z25--z28--z29; draw z19..{up}z18--z23; 
   draw z31--z33; draw z30--z32; draw z23--z22--z21--z20;
   draw z26{up}..{dir150}z30; draw z14--z27{up}..z34; draw z11--z17--z18;
endchar;      

beginchar(98+o,U#,U#,0); "kuei - the sandal"; pickup thick_pen;
   z1=(a,0); z2=(4.5a,0); z3=(8a,0); z4=(7.6a,.3b); z5=(2a,1.5b);
   z7=(7a,1.5b); z8=(6.6a,1.8b); z9=(1.5a,3b); z11=(7.5a,3b);
   z12=(7.1a,3.3b); z13=(3a,4.5b); z15=(6a,4.5b); z16=(4.5a,5.5b);
   z17=(a,4b); z18=(2a,7b); z19=(4.5a,8b); z20=(5.5a,7b);
   z21=(8a,4b); z22=(8a,7b); z23=(7.6a,7.3b); draw z1--z3--z4;
   draw z5--z7--z8; draw z9--z11--z12; draw z13--z15; draw z2--z16;
   draw z18--z22--z23; draw z17{dir30}..z19; draw z20..{dir-30}z21;
endchar;

beginchar(99+o,U#,U#,0); "lou - the reaping maid"; pickup thick_pen;
   z1=(2.1a,2.5b); z2=(2.5a,3b); z3=(2.5a,4b); z4=(6.5a,4b); z5=(6.5a,3b);
   z6=(2.1a,5.5b); z7=(2.5a,6b); z8=(2.5a,7b); z9=(6.5a,7b); z10=(6.5a,6b);  
   z11=(4.5a,8b); z12=(4.5a,3.5b); z13=(3a,b); z14=(2a,0); z15=(6a,2b);
   z16=(3.9a,1.4b); z17=(6.5a,0); z18=(a,2b); z19=(8a,2b); z20=(7.6a,2.3b);
   draw z1..{up}z2--z3--z4--z5--z2; draw z6..{up}z7--z8--z9--z10--z7;                    
   draw z11--z12{down}..z13; draw z14{right}..z15; draw z18--z19--z20;
   draw z16{dir-20}..{dir-60}z17; draw (1.5a,5b)--(7.5a,5b);
endchar;

beginchar(100+o,U#,U#,0); "wey - the corn guard"; pickup thick_pen;
   z1=(2a,0); z2=(2a,2b); z3=(2a,3b); z4=(2a,4b); z5=(5.2a,b); z6=(7a,2b);
   z7=(7a,3b); z8=(7a,4b); z9=(7.5a,4b); z10=(8a,5b); z11=(8a,6.5b);
   z12=(8a,8b); z13=(4.5a,5b); z14=(4.5a,8b); z15=(a,5b); z16=(a,6.5b);
   z17=(a,8b); z18=(6.3a,0); draw z16--z11;
   draw z1--z4--z8--z6{down}..{left}z18..{dir105}z5; draw z2--z6;
   draw z3--z7; draw z9..{up}z10--z12--z17--z15--z10; draw z13--z14;
endchar;

beginchar(101+o,U#,U#,0); "mao - the setting sun"; pickup thick_pen;   
   z1=(2a,0); z2=(4a,3b); z3=(4a,4.2b); z4=(3a,2b); z6=(a,b); z5=.5[z4,z6];
   z7=(2a,4.5b); z8=(2a,4b); z9=(4a,5b); z10=(5.5a,0); z11=(5.5a,4b);
   z12=(5.5a,4.5b); z13=(8a,4.5b); z14=(8a,1.5b); z15=(7a,.5b);
   z16=(2.5a,5b); z17=(2.5a,8b); z18=(6.5a,5b); z19=(6.5a,8b);
   z20=(2.5a,5.5b); z21=(6.5a,5.5b); z22=(2.5a,6.5b); z23=(6.5a,6.5b);
   z24=(2.5a,7.5b); z25=(6.5a,7.5b); z26=(7.4a,2.5b);
   draw z1{right}..{up}z2--z3; draw z4--z6; draw z5--z7; draw z8--z9;
   draw z10--z12; draw z11{dir-20}..{dir20}z13{dir-160}..z26..{down}z14..z15;
   draw z16--z17; draw z18--z19; draw z20--z21; draw z22--z23; draw z24--z25;
endchar;

beginchar(102+o,U#,U#,0); "py - the net"; pickup thick_pen; 
   z1=(4.5a,0); z2=(1.6a,4b); z3=(4.5a,8b); z4=(6.8a,8b); z5=(6.8a,5.3b);
   z6=(2.2a,5.3b); z7=(2.2a,8b); z8=(2.2a,6.7b); z9=(6.8a,6.7b);
   z10=(7.4a,4b); z11=(7a,4.3b); z12=(a,2.7b); z13=(8a,2.7b);
   z14=(7.6a,3b); z15=(2a,1.3b); z16=(7a,1.3b); z17=(6.6a,1.6b);
   z20=(3a,2.7b); z21=(3a,4.8b); z22=(6a,2.7b); z23=(6a,4.8b);
   draw z1--z3--z4--z5--z6--z7--z3; draw z8--z9; draw z2--z10--z11;
   draw z12--z13--z14; draw z15--z16--z17; draw z20--z21; draw z22--z23;
endchar;

beginchar(103+o,U#,U#,0); "tsui - the mouth"; pickup mean_pen;  
   z1=(3a,0); z2=(4a,2b); z3=(5.5a,2b); z4=(7a,2b); z5=(4a,3b); z6=(7a,b);
   z7=(7a,3b); z8=(4a,4b); z9=(5.5a,4b); z10=(7a,4b); z11=(6.2a,0);
   z12=(5.5a,.4b); z13=(a,2b); z14=(a,3b); z15=(a,6.5b); z16=(2.5a,3b);
   z17=(2.5a,4.5b); z18=(2.5a,6.5b); z19=(5a,5b); z20=(6.3a,5b);
   z21=(8a,6b); z22=(7.5a,5b); z23=(6a,5b); z24=(6a,7b); z25=(6a,8b);
   z26=(4.5a,7b); z27=(4.5a,8b); z28=(3.7a,5.4b); z29=(3.7a,7b);
   z30=(4.5a,6b); z31=(7a,7b);
   draw z1..{up}z2--z8--z10--z6{down}..{left}z11..z12; draw z2--z4;
   draw z5--z7; draw z3--z9; draw z13--z15{dir-20}..{dir20}z18--z16;
   draw z14--z16{right}..z8--z19--z9--z20; draw z28--z29; draw z24--z26;
   draw z17{right}..z28--z30--z27; draw z25--z23--z22{right}..{dir60}z21;
   pickup thin_pen draw z30{dir20}..z31;
endchar;

beginchar(104+o,U#,U#,0); "tsan - the grandeur"; pickup thick_pen; 
   z1=(a,2.5b); z2=(4.5a,6.5b); z3=(8a,1.5b); z4=(5a,8b); z5=(3.2a,7b);
   z6=(1.5a,5b); z7=(7.5a,5b); z8=(7.1a,5.3b); z9=(2a,7b); z10=(7a,7b);
   z11=(6.6a,7.3b); z12=(2a,2b); z13=(4.5a,4b); z14=(1.5a,b); z15=(5a,3b);
   z16=(a,0); z17=(5.5a,2b); z18=(4.7a,5.5b);
   draw z1{dir30}..{up}z2{dir-70}..{dir-30}z3; draw z9--z10--z11;
   draw z8--z7--z6{right}..z5..{right}z4; pickup mean_pen;
   draw z12{dir20}..z13; draw z14{dir10}..z15; draw z16{right}..z17;
   draw z18{right}..z10;
endchar;

beginchar(105+o,U#,U#,0); "tsyng - the fountain"; pickup thick_pen;  
   z1=(a,0); z2=(3a,2.5b); z3=(3a,7b); z4=(6a,0); z5=(6a,8b); z6=(a,2.5b);
   z7=(8a,2.5b); z8=(7.6a,2.8b); z9=(a,5.5b); z10=(7.5a,5.5b);
   z11=(7.1a,5.8b); z12=(2.5a,7.5b); draw z1..{up}z2--z3--z12; 
   draw z4--z5; draw z6--z7--z8; draw z9--z10--z11;
endchar;

beginchar(106+o,U#,U#,0); "kwei - the maanan"; pickup thick_pen; 
   z1=(a,.5); z2=(3.5a,2.5b); z3=(4.5a,3b); z4=(4.5a,0); z5=(8a,0);
   z6=(7.6a,.3b); z7=(7a,1.5b); z8=(6a,2.5b); z9=(2a,3b); z10=(4a,3.5b);
   z11=(6a,3.5b); z12=(2a,5b); z13=(6a,5b); z14=(2a,7b); z15=(3.5a,8b);
   z16=(6.5a,7b); draw z1{dir20}..z2; draw z3--z4--z5--z6; draw z4--z7;
   draw z4--z8; draw z15..{left}z14--z9{dir30}..{right}z10--z11--z13{up}%%
   ..{dir60}z16--z14; draw z12--z13; draw z10--(4a,7b);
endchar;

beginchar(107+o,U#,U#,0); "liu - the willow"; pickup mean_pen;
   z1=(2.5a,0); z2=(2.5a,4.5b); z3=(2.5a,5.5b); z4=(2.5a,8b); z5=(a,2.5b);
   z6=(a,5.5b); z7=(4a,5.5b); z8=(3.2a,2.5b); z10=(5.05a,3.2b); z9=(4a,3b);
   z11=(3.5a,0); z12=(5.05a,6.5b); z13=(4a,7b); z14=(4a,7.5b); z15=(5a,8b);
   z16=(6a,0); z17=(6a,7b); z18=(8a,7b); z19=(8a,3.5b); z20=(6.7a,1.5b);
   draw z1--z4; draw z5..{up}z3; draw z6--z7; draw z9--z14; draw z13--z15;
   draw z8--z10; draw z11..{up}z10--z12; draw z2--(4a,3.5b);
   draw z16--z17{dir-30}..{dir30}z18--z19{down}..z20;
endchar;

beginchar(108+o,U#,U#,0); "sing - the star"; pickup thick_pen; 
   z1=(a,0); z3=(8a,0); z4=(7.6a,.3b); z8=(3.3a,3.2b); z9=(7a,3.2b);
   z10=(6.6a,3.5b); z2=.5[z1,z3]; z20=.5[z1,z10]; z5=.15[.5[z1,z8],z20]; 
   z6=.2[.5[z3,z9],z20]; z7=.2[.5[z4,z10],z20]; z11=(a,b); 
   z12=(2.5a,4.5b); z13=(2.5a,5.4b); z14=(6.5a,5.4b); z15=(2.5a,8b); 
   z16=(6.5a,8b); z17=.5[z13,z14]; z18=.5[z13,z15]; z19=.5[z14,z16];
   draw z2--z17--z14--z16--z15--z13--z17; draw z18--z19; draw z1--z3--z4;
   draw z5--z6--z7; draw z8--z9--z10; draw z11{dir60}..{up}z12;
endchar;

beginchar(109+o,U#,U#,0); "tchang - the net"; pickup thick_pen; 
   z1=(a,0); z2=(2.5a,2b); z3=(3a,4b); z4=(1.5a,3b); z5=(1.5a,6.5b);
   z6=(3a,6b); z7=(3a,8b); z8=(3a,7.5b); z9=(a,7.5b); z10=(4a,3.4b);
   z11=(8a,3.4b); z12=(7.6a,3.7b); z13=(4.5a,4.8b); z14=(7.3a,4.8b);
   z15=(6.9a,5.1b); z16=(4.5a,6.2b); z17=(6.8a,6.2b); z18=(6.4a,6.5b);
   z19=(4.5a,7.7b); z20=(8a,7.7b); z21=(7.6a,8b); z22=(5a,3.4b);
   z23=(8a,0); z24=(4a,2b); z25=(3a,.8b); z26=(7.4a,2.5b); z27=(6.8a,3.4b);
   draw z1..z2{z3-z2}..z3; draw z4..z3{z3-z4}..{down}z10..z24{down}..z25;
   draw z4--z5--z6--z7; draw z8--z9; draw z25{dir20}..z26--z27;
   draw z10{dir45}..{up}z13--z19--z20--z21; draw z16--z17--z18; 
   draw z13--z14--z15; draw z10--z11--z12; z28=(5.6a,1.8b);
   draw z28{z23-z22}..z23; pickup thin_pen; draw z28{z22-z23}..z22;
endchar;

beginchar(110+o,U#,U#,0); "yih - the wing"; pickup thick_pen; 
   z1=(1.5a,0); z2=(3.1a,3b); z3=(6a,3b); z4=(7a,0); z5=(a,2b); z6=(1.5a,3b);
   z7=(1.7a,4b); z8=(1.5a,5b); z9=(a,6b); z10=(7a,3b); z11=(6.7a,4b);
   z12=(7.3a,5.4b); z13=(a,b); z14=(3.1a,1.5b); z15=(8a,b); z16=(7.6a,1.3b);
   z17=(7.4a,2b); z18=(7a,2.3b); z19=(4.5a,3b); z20=(4.5a,5b); z21=(a,6b);
   z22=(4a,6b); z23=(5a,6b); z24=(8a,6b); z25=(a,7b); z26=(4a,7b);
   z27=(5a,7b); z28=(8a,7b); z29=(a,8b); z30=(4a,8b); z31=(5a,8b); 
   z32=(8a,8b); draw z1{dir30}..z14{up}..z2; draw z3{dir-150}..{dir-30}z4;
   draw z5..z6..z7..z8..z9; draw z10..z11..z12; draw z19--z20; draw z25--z26;
   draw z27--z28; draw z13--z15--z16; draw z5--z17--z18; draw z6--z10;
   draw z7--z11; draw z8--(7a,5b); draw z21--z22--z30--z29; draw z23--z24%%
   --z32--z31;
endchar;

beginchar(111+o,U#,U#,0); "tschen - the wagon"; pickup thick_pen;     
   z1=(2.5a,0); z2=(2.5a,8b); z3=(a,2b); z4=(4a,2b); z5=(3.6a,2.3b); 
   z6=(a,7b); z7=(4a,7b); z8=(3.6a,7.3b); z9=.25[z3,z6]; z10=.5[z3,z6];
   z11=.75[z3,z6]; z12=.25[z4,z7]; z13=.5[z4,z7]; z14=.75[z4,z7];
   z15=(4.4a,5b); z16=(6.2a,8b); z17=(8a,5b); z18=(4.9a,3.8b);
   z19=(6.4a,5.7b); z20=(4.9a,2b); z21=(6.8a,4b); z22=(4a,0); 
   z23=(8a,2.5b); draw z1--z2; draw z3--z4--z5; draw z6--z7--z8;
   draw z13--z10--z11--z14--z12--z9--z10; draw z20{dir15}..z21;
   draw z15{dir20}..{dir60}z16{dir-70}..z17; draw z18{dir15}..z19;
   draw z22{right}..z23;
endchar;

beginchar(112+o,U#,U#,0); "kio - the horn"; pickup thick_pen;
   z1=(a,0); z2=(2.7a,2b); z3=(3a,3.5b); z4=(3a,5.5b); z5=(5.5a,2b);
   z6=(5.5a,5b); z7=(6a,.5b); z8=(7.2a,0); z9=(8a,b); z10=(8a,2b);
   z11=(8a,3.5b); z12=(8a,5.5b); z13=(5.5a,5.5b); z14=(6.6a,7.2b); 
   z15=(7a,8b); z16=(a,4.5b); z17=(4a,7.2b); z18=(4.6a,8b);
   draw z1{dir45}..z2..{up}z3--z4; draw z4..z6..z12; draw z3--z11;
   draw z2--z10; draw z12--z9{down}..{left}z8..z7; draw z5--z6;
   draw z13--z15; draw z16{dir20}..{z18-z17}z17--z18; draw z17--z14;
endchar;

beginchar(113+o,U#,U#,0); "kang - the neck"; pickup thick_pen; 
   z1=(a,0); z2=(3a,2.5b); z3=(3a,5.5b); z4=(3a,6b); z5=(5.7a,6b);
   z6=(5.4a,5.5b); z7=(5a,4b); z8=(5a,1.5b); z9=(6a,0); z10=(8a,0);
   z11=(8a,2.5b); z12=(a,7b); z13=(4a,7b); z14=(8a,7b); z15=(7.6a,7.3b);
   z16=(4a,8b); draw z1{dir30}..{up}z2--z4; draw z12--z14--z15;
   draw z5..{z6-z5}z6..{down}z7--z8{down}..{right}z9--z10; 
   draw z3--z6; draw z13--z16; pickup mean_pen; draw z10{dir110}..z11; 
endchar;

beginchar(114+o,U#,U#,0); "ti - the foundation"; pickup thick_pen;
   z1=(a,0); z3=(4a,1.5b); z2=.5[z1,z3]; z4=(4.2a,0); z14=.9[z1,z3];
   z5=(2.5a,4.5b); z6=(2.5a,7b); z7=(2.5a,7.5b); z9=(7a,8b);
   z8=1/2[z6,z9]; z10=(7a,0); z11=(8a,2b); z12=(7.5a,4.5b); z13=(7.1a,4.8b);
   draw z1--z3; draw z2--z7; draw z6--z9; draw z5--z12--z13;
   draw z8{down}..{dir-40}z10; pickup thin_pen;
   draw z10{dir80}..{dir60}z11; draw z14{dir-40}..z4;
endchar;

beginchar(115+o,U#,U#,0); "fang - the chamber"; pickup thick_pen; 
   z1=(a,0); z2=(2.5a,3b); z3=(2.5a,5.7b); z4=(2.4a,6.8b); z5=(2.2a,8b);
   z6=(a,8b); z7=(8a,8b); z8=(7.6a,8.3b); z9=(7a,6.8b); z10=(7a,5.7b);
   z11=(4.7a,5.7b); z12=(4.7a,4.5b); z13=(4a,4.5b); z14=(2a,0); 
   z15=(8a,4.5b); z16=(7.6a,4.8b); z17=(7a,3.5b); z19=(6a,b); 
   z18=.2[z17,z19]; z20=(5a,0); z21=(4a,.5b); z22=(a,4.5b); z23=(4a,3b);
   draw z1..{up}z2--z3{up}..z5; draw z6--z7--z8; draw z4--z9;
   draw z3--z10; draw (7.1a,7b){dir-100}..{dir-80}(7.1a,5.5b);
   draw z11--z12; draw z22--z15--z16; draw z23--z18; draw z13..z23{down}%%
   ..z14; draw z17--z19{z19-z17}..{left}z20..z21;
endchar;

beginchar(116+o,U#,U#,0); "sin - the heart"; pickup thick_pen;
   z1=(a,1.5b); z2=(1.5a,3b); z3=(1.6a,5b); z4=(2.5a,6b); z5=(3a,2b);
   z6=(4.5a,.3b); z7=(7.5a,0); z8=(6.5a,3b); z9=(3a,8b); z10=(4.5a,5b);
   z11=(6.5a,8b); z12=(6.7a,7b); z13=(8a,3b);
   draw z4--z5{z5-z4}..{z7-z6}z6--z7; draw z7..{up}z8; draw z12..{down}z13;
   draw z9..{dir-70}z10{dir70}..z11; pickup mean_pen; 
   draw z3--z2{z2-z3}..z1;
endchar;

beginchar(117+o,U#,U#,0); "wi - the tail"; pickup thick_pen;
   z1=(a,0); z2=(2.5a,2.5b); z3=(2.8a,4b); z4=(3a,5.5b); z5=(3a,6.5b);
   z6=(3a,8b); z7=(7.5a,5.5b); z8=(7.1a,5.8b); z9=(7.2a,6.5b);
   z10=(7.2a,8b); z12=(8a,2.5b); (x11,y11)=(1/2[x8,x12],4b); z13=(5a,5.5b);
   z14=(5a,b); z15=(6a,0); z16=(8a,0); z17=(7.5a,2b);
   draw z1{dir60}..{up}z4--z6--z10{dir-110}..{dir-70}z9--z5; draw z2--z12;
   draw z4--z7--z8; draw z13--z14{down}..{right}z15--z16; draw z3--z11;
   pickup thin_pen; draw z16{dir160}..z17; draw z8--z12;
endchar;

beginchar(118+o,U#,U#,0); "ki - the dungbasket"; pickup thick_pen;
   z1=(1.5a,0); z2=(3.5a,1.5b); z3=(6a,1.5b); z4=(8a,0); z5=(a,2b);
   z6=(3a,2b); z7=(6a,2b); z8=(8a,2b); z9=(7.6a,2.3b); z10=(3a,6b);
   z11=(6a,6b); z12=(a,5b); z13=(8a,5b); z14=(7.6a,5.3b); z15=1/4[z6,z10];
   z16=1/4[z7,z11]; z17=.5[z6,z10]; z18=.5[z7,z11]; z19=(1.5a,5.5b);
   z20=(3.5a,8b); z21=(4.5a,5.5b); z22=(6.5a,8b); z23=(5a,7b); z24=(8a,7b);
   z25=(4.2a,6b); z26=(7.2a,6b); z27=2/3[z19,z20]; z28=2/3[z21,z22];
   draw z1{dir30}..z2; draw z3{dir-30}..z4; draw z5--z8--z9; draw z15--z16;
   draw z17--z18; draw z12--z13--z14; draw z6--z10; draw z7--z11;
   draw z19{dir40}..z20; draw z21{dir40}..z22; draw z27{dir-10}..z23;
   draw z28{dir-10}..z24; draw z27{right}..z25; draw z28{right}..z26;
endchar;

beginchar(119+o,U#,U#,0); "tou - the bushel"; pickup thick_pen;
   z1=(a,2.7b); z2=(8a,5b); z3=(7.6a,5.2b); z4=(5a,0); z5=(5a,8b);
   z6=(2.3a,4.3b); z7=(1.5a,6b); z8=(3.5a,6b); z9=(2.5a,7.5b);
   draw z1--z2--z3; draw z4--z5; pickup mean_pen;
   fill fullcircle xscaled .8pt yscaled .5pt rotated 150 shifted z6; 
   draw z6{dir60}..z7;
   fill fullcircle xscaled .8pt yscaled .5pt rotated 150 shifted z8; 
   draw z8{dir60}..z9;   
endchar;

beginchar(120+o,U#,U#,0); "niu - the cattle"; pickup thick_pen;
   z1=(a,3.5b); z3=(2.8a,5.5b); z4=(3a,7b); z5=(4.5a,8b);
   z6=(6.2a,5.5b); z7=(6a,6b); z8=(4.5a,0); z9=(8a,3.5b); z10=(7.5a,4b);
   draw z1..z3..{up}z4; draw z5..z8; draw z3..z6; draw z6..z7;
   draw z1..z9; draw z9..z10;
endchar;

beginchar(121+o,U#,U#,0); "nue - the virgin"; pickup thick_pen;      
   z1=(a,0); z2=(5a,1.5b); z3=(6a,5b); z4=(3a,2b); z5=(5.5a,8b);
   z6=(a,5b); z7=(8a,5b); z8=(7.6a,5.3b); z9=1/6[z4,z5]; z10=(8a,0);
   draw z1{dir10}..z2{z3-z1}..{dir80}z3; draw z9{right}..z10;
   draw z4--z5; draw z6--z7--z8;
endchar;

beginchar(122+o,U#,U#,0); "hue - the mound"; pickup mean_pen;
   z1=(a,0); z2=(2a,3b); z3=(2a,5b); z4=(2a,6b); z5=(1.5a,7b); z6=(2.5a,0);
   z7=(3a,1.5b); z8=(2.5a,2.8b); z9=(3.8a,0); z10=(3.8a,1.5b); 
   z11=(3.8a,3.5b); z12=(4.5a,4.05b); z15=(4.5a,7b); z16=(4.5a,8b); 
   z17=(6.5a,7b); z18=(6.1a,7.3b); z19=(8a,6b); z20=(7.1a,5.5b); 
   z21=(6.5a,5.6b); z22=(7a,5b); z23=(6.7a,4.4b); z24=(5.2a,3.5b); 
   z25=(5a,3b); z26=(6.7a,2.6b); z27=(6.5a,2b); z28=(6.5a,b); z29=(5.2a,2b); 
   z30=(5.2a,0); z31=(8a,0); z32=(7.6a,.3b); 
   draw z1{dir50}..{up}z2--z4{dir140}..z5; draw z15--z17--z18; draw z3--z22; 
   draw z4--z19{down}..{left}z20; draw z7--z10;   
   draw z16--z12--z24--z30--z28--z26{up}..{z25-z26}z24; draw z29--z27; 
   draw z2..z8..{down}z7..{dir-120}z6--z31--z32; draw z7--z10; 
   draw z9--z11{up}..{right}z23{dir140}..{up}z21;
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%END OF FILE CHINASYM.STA%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%

