%% This file is part of the CHINASYM package, 
%% a PACKAGE for writing CHINESE CALENDAR SYMBOLS 
%% with LaTeX version 2e. 
%% 
%% -- Copyright 1997 by Udo Heyl, Eisenach. All rights reserved. -- 
%% 
%% Copying of this file is authorized only if either 
%% (1) you make absolutely no changes to your copy, including name, or 
%% (2) if you do make changes, you name it something other than 
%%          CHINASYM.ADD,     CHINASYM.ALF,     CHINASYM.ELE,
%%          CHINASYM.NUM,     CHINASYM.SBL,     CHINASYM.STA,
%%          CHINA10.MF   or   CHINA2E.STY !!!
%% This restriction helps ensure that all styles developed for 
%% DANTE e.V. (the users group of german speaking TeX users) 
%% remain identical. 
%% 
%% You are NOT ALLOWED to distribute this file alone. You are NOT 
%% ALLOWED to take money for the distribution or use of either this 
%% file or a changed version, except for some nominal charge for 
%% copying etc. 
%% 
%% Error Reports in case of UNCHANGED versions to 
%% 
%% Udo Heyl
%% Stregdaer Allee 7
%% 99817 Eisenach
%% Federal Republic of Germany 
%% 
%% or 
%% DANTE, Deutschsprachige Anwendervereinigung TeX e.V. 
%% Postfach 10 18 40 
%% 69008 Heidelberg 
%% Federal Republic of Germany 
%% Email: german@dante.de 
%% 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% This file contains the alphabetic and numeric signs %%
%% of the european languages (UPPER-CASE only) for use %%
%% in CHINESE mode without switch back to COMMON mode! %%
%% written by Udo Heyl, Eisenach (Vers.1.0) 97/05/15.  %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% The stops

beginchar(33,.6U#,U#,0); "exclamation mark"; pickup thick_pen; 
   z1=(3.1a,2b); z2=(2.5a,7b); z3=(5a,7b); fill z1--z2--z3..cycle;
   fill fullcircle scaled 1pt shifted (3a,.5b);
endchar;

beginchar(34,.4U#,U#,0); "doublequote"; pickup thin_pen;
   z1=(a,6.5b); z2=(2.5a,6.5b);
   fill fullcircle scaled 1pt shifted z1;
   draw (x1+.3a,y1+.2b){dir-60}..{dir-160}(x1,y1-1.6b);   
   fill fullcircle scaled 1pt shifted z2;
   draw (x2+.3a,y2+.2b){dir-60}..{dir-160}(x2,y2-1.6b);   
endchar;

beginchar(35,U#,U#,0); "address mark"; pickup thick_pen;
   z1=(a,0); z2=(a,5b); z3=(8a,5b); z4=(8a,0); z5=1/2[z1,z3];
   z6=1/4[z5,z2]; z7=1/4[z5,z3]; draw z2--z5--z3--z4--z1--z2--z3;
   pickup thin_pen; draw z1--z6; draw z4--z7;
endchar;

beginchar(36,.6U#,U#,0); "dollar"; pickup thick_pen; 
   z1=(5a,7b); z2=(5a,5.5b); z3=(3a,7b); z4=(a,5.25b); z5=(3a,3.5b);
   z6=(5a,1.75b); z7=(3a,0); z8=(a,1.5b); z9=(a,0); z10=(2a,6.7b);
   z11=(2a,3.8b); z12=(4a,3.2b); z13=(4a,.3b); z14=(2.5a,8b);
   z15=(3.5a,8b); z16=(2.5a,-b); z17=(3.5a,-b);
   draw z1{dir-120}..z2; draw z9{dir60}..z8; draw z2..{left}z3..z10%%
   ..{down}z4..z11..{right}z5..z12..{down}z6..z13..{left}z7..z8;
   pickup thin_pen; draw z14--z16; draw z15--z17;
endchar;

beginchar(37,.6U#,U#,0); "percent"; pickup thick_pen;
   z1=(2a,6b); z2=(3a,b); draw (a,0)--(4a,7b);
   draw fullcircle xscaled 2pt yscaled 1pt rotated 50 shifted z1;
   draw fullcircle xscaled 2pt yscaled 1pt rotated 50 shifted z2;
endchar;

beginchar(38,.6U#,U#,0); "and"; pickup thick_pen;
   z1=(5a,5b); z2=(1.9a,4b); z3=(4.8a,3b); z4=(1.7a,2b); z5=(4.6a,b); 
   z6=(3.6a,6b); z7=(3a,0); 
   draw z1{dir190}..{dir-100}z2{dir-80}..{dir-10}z3{dir190}..%%
   {dir-100}z4{dir-80}..{dir-10}z5; pickup thin_pen; draw z6--z7;
endchar;

beginchar(39,.2U#,U#,0); "quote"; pickup thin_pen;
   z1=(a,6.5b);
   fill fullcircle scaled 1pt shifted z1;
   draw (x1+.3a,y1+.2b){dir-60}..{dir-160}(x1,y1-1.6b);   
endchar;

beginchar(40,.4U#,U#,0); "bracket on"; pickup thick_pen;      
   z1=(2a,0); z2=(3a,7b); z3=(1.5a,3.5b);
   draw z1..{z2-z1}z3..z2;
endchar;

beginchar(41,.4U#,U#,0); "bracket off"; pickup thick_pen;      
   z1=(a,0); z2=(2a,7b); z3=(2.5a,3.5b);
   draw z1..{z2-z1}z3..z2;
endchar;

beginchar(42,U#,U#,0); "asterix"; pickup thick_pen;
   z1=(a,2b); z2=(4a,0); z3=(7a,2b); z4=(8a,5b); z5=(5a,7b); z6=(2a,5b);
   z7=2/5[z2,z4]; z8=.775[z5,z3]; z9=2/5[z5,z3]; z10=2/5[z5,z1];
   z11=.3625[z1,z5]; z12=2/5[z2,z6]; z13=1/6[z7,z10]; z14=1/6[z8,z11];
   z15=1/6[z9,z12]; z16=1/6[z10,z7]; z17=1/6[z11,z8]; z18=1/6[z12,z9];
   draw z2--z13--z3--z14--z4--z15--z5--z16--z6--z17--z1--z18--z2;
endchar;

beginchar(43,.6U#,U#,0); "plus"; pickup thick_pen;
   z1=(a,3b); z2=(5a,3b); z3=(2.6a,0); z4=(3.4a,6b);
   draw z1--z2; draw z3--z4;
endchar;

beginchar(44,.2U#,U#,0); "comma"; pickup thin_pen;
   z1=(a,b);
   fill fullcircle scaled 1pt shifted z1; pickup thin_pen;
   draw (x1+.3a,y1+.2b){dir-60}..{dir-160}(x1,y1-1.6b);   
endchar;

beginchar(45,.6U#,U#,0); "minus"; pickup thick_pen;
   z1=(a,3b); z2=(5a,3b); draw z1--z2;
endchar;

beginchar(46,.2U#,U#,0); "point"; pickup thick_pen;
   fill fullcircle scaled 1pt shifted (a,.5b);
endchar;

beginchar(47,.6U#,U#,0); "slash"; pickup thick_pen;
   draw (a,0)--(4a,7b);
endchar;

beginchar(58,.4U#,U#,0); "double point"; pickup thick_pen;
   fill fullcircle scaled 1pt shifted (1.5a,.5b);
   fill fullcircle scaled 1pt shifted (2.1a,4.5b);
endchar;

beginchar(59,.4U#,U#,0); "semicolon"; pickup thick_pen;
   z1=(1.5a,b);
   fill fullcircle scaled 1pt shifted z1; pickup thin_pen;
   draw (x1+.3a,y1+.2b){dir-60}..{dir-160}(x1,y1-1.6b);   
   fill fullcircle scaled 1pt shifted (2.1a,4.5b);
endchar;

beginchar(60,U#,U#,0); "info"; pickup thick_pen;
   z1=(a,0); z2=(8a,0); z3=(8a,8b); z4=(a,8b); z5=(3a,1.5b); z6=(6a,1.5b);
   z7=1/2[z5,z6]; z9=(4.5a,6b); z8=5/8[z7,z9]; z10=(3.8a,3.7b);
   draw z5--z6; draw z7--z8--z10; fill fullcircle scaled 1.5pt shifted z9;
   pickup tiny_pen; draw z1--z2--z3--z4--z1;
endchar;

beginchar(61,.7U#,U#,0); "equal"; pickup thick_pen;
   z1=(a,2.75b); z2=(5.6a,2.75b); z3=(1.4a,4.75b); z4=(6a,4.75b);
   draw z1--z2; draw z3--z4;
endchar;

beginchar(62,U#,U#,0); "quest"; pickup thick_pen;
   z1=(a,0); z2=(8a,0); z3=(8a,8b); z4=(a,8b); z5=(4.5a,3.5b); 
   z6=(6.5a,5.5b); z7=(4.5a,7b); z9=(4.5a,2b); z8=(2.5a,5.5b);
   draw z5{up}..z6{up}..z7{left}..{down}z8; 
   fill fullcircle scaled 1.5pt shifted z9;
   pickup tiny_pen; draw z1--z2--z3--z4--z1;
endchar;

beginchar(63,.7U#,U#,0); "question mark"; pickup thick_pen;
   z2=(2.8a,2b); z3=(6a,5b); z4=(4a,7.5b); z5=(a,6b); z1=14/11[z4,z2];
   draw z2{z4-z2}..z3{z4-z2}..z4{left}..{z2-z4}z5;
   fill fullcircle scaled 1pt shifted z1; 
endchar;

beginchar(64,U#,U#,0); "telephone"; pickup syma_pen;
   z1=(a,0); z2=(2a,2b); z3=(4.5a,1.4b); z4=(7a,2b); z5=(8a,0);
   z6=(4.5a,3.86b); z7=(a,5b); z8=(3a,6b); z9=(4.5a,7b);
   z11=(6a,6b); z12=(8a,5b); z13=(6a,8b); z14=(3a,8b);
   fill z1{up}..{dir45}z2{dir-30}..{right}z3{right}..{dir30}z4{dir-45}%%
   ..{down}z5{left}--z1..cycle; fill z7{dir45}..{dir20}z8{up}..z9..{down}%%
   z11{dir-20}..{dir-45}z12{up}..{left}z13--z14{left}..{down}z7..cycle;
   draw fullcircle xscaled 5pt yscaled 3pt shifted z6;
   fill fullcircle xscaled 2pt yscaled 1.2pt shifted z6;
endchar;

beginchar("A",.7U#,U#,0); "A"; pickup thick_pen;
   z1=(a,0); z2=(2a,5b); z3=(4.8a,7.5b); z4=(6a,5b); z5=(5a,0); z7=(a,4b);
   z6=.4[z4,z5]; draw z1--z2{z2-z1}..{right}z3..{z1-z2}z4--z5;
   draw z6{left}..{dir135}z7;
endchar;

beginchar("B",.7U#,U#,0); "B"; pickup thick_pen;
   z1=(1.5a,0); z3=(3a,7.5b); z2=8/15[z1,z3]; z4=(a,6.5b); z5=(5a,5.5b);
   z6=(5.5a,2b); z7=(4a,0); z8=(a,0);
   draw z1--z3; draw z4{dir45}..{right}z3..{z1-z3}z5..{left}z2{right}%%
   ..{z1-z3}z6..{left}z7--z8;
endchar;

beginchar("C",.7U#,U#,0); "C"; pickup thick_pen;
   z1=(6a,6.5b); z2=(5a,7.5b); z3=(3.5a,7b); z4=(a,3b); z5=(2.5a,0);
   z6=(4.5a,0); z7=(6a,b);
   draw z1..{left}z2..z3..z4..{right}z5--z6{right}..z7;
endchar;

beginchar("D",.7U#,U#,0); "D"; pickup thick_pen;
   z1=(a,6.5b); z2=(3a,7.5b); z3=(5.5a,5.5b); z4=(5.5a,2b); z5=(4a,0);
   z6=(1.5a,0); z7=(a,0); draw z2--z6; 
   draw z1..{right}z2..z3..z4..{left}z5--z7;
endchar;

beginchar("E",.7U#,U#,0); "E"; pickup thick_pen;
   z1=(a,0); z2=(2.5a,7.5b); z3=(a,7b); z4=(a,4b); z5=(6a,7.5b); z6=(4a,4b);
   z7=(4a,0); z8=(6a,b);
   draw z1--z2; draw z4{dir30}..{dir30}z6; draw z3..z2{right}..{dir30}z5;
   draw z1--z7{right}..{up}z8;
endchar; 

beginchar("F",.7U#,U#,0); "F"; pickup thick_pen;
   z1=(a,0); z2=(2.5a,7.5b); z3=(a,7b); z4=(a,4b); z5=(6a,7.5b); z6=(4a,4b);
   draw z1--z2; draw z4{dir30}..{dir30}z6; draw z3..z2{right}..{dir30}z5;
endchar; 

beginchar("G",.7U#,U#,0); "G"; pickup thick_pen;
   z1=(6a,6.5b); z2=(5a,7.5b); z3=(3.5a,7b); z4=(a,3b); z5=(2.5a,0);
   z6=(4.5a,0); z7=(5.5a,2b); z8=(5a,4b); z9=(4a,4b); z10=(6a,4.5b);
   draw z1..{left}z2..z3..z4..{right}z5--z6{right}..z7{z2-z5}..z8;
   draw z9..z8..z10;
endchar;

beginchar("H",.7U#,U#,0); "H"; pickup thick_pen;
   z1=(a,0); z2=(2.5a,7.5b); z3=(4.5a,0); z4=(6a,7.5b); z5=8/15[z3,z4];
   z6=(a,5b); draw z1--z2; draw z3--z4; draw z6{dir-45}..{right}z5;
endchar;

beginchar("I",.6U#,U#,0); "I"; pickup thick_pen;
   z1=(a,0); z2=(3a,0); z3=(5a,0); z5=(2a,6b); 
   z6=(4.5a,7.5b); z7=6/7[z2,z6]; draw fullcircle scaled .4pt shifted z6;
   draw z1--z3; draw z2--z7; draw z5{right}..{dir60}z7;
endchar;

beginchar("J",.7U#,U#,0); "J"; pickup thick_pen;
   z1=(a,2b); z2=(3a,7.5b); z3=(1.5a,7b); z4=(2a,0); z5=(6a,7.5b);
   z6=(3.5a,0); z7=(5a,2.5b);
   draw z3..z2{right}..{dir30}z5--z7{z7-z5}..{left}z6--z4{left}..z1;
endchar;

beginchar("K",.7U#,U#,0); "K"; pickup thick_pen;
   z1=(a,0); z2=(2.5a,7.5b); z3=(a,6.5b); z4=8/15[z1,z2]; z5=(5a,7b);
   z6=(4.5a,0); z7=(6a,b);
   draw z1--z2; draw z3{right}..z2; draw z4{right}..z5;
   draw z4{right}..{right}z6..z7;
endchar;

beginchar("L",.7U#,U#,0); "L"; pickup thick_pen;
   z3=(5a,6.5b); z4=(4a,7.5b); z5=(2a,5b); z6=(a,0); z7=(4a,0); z8=(6a,b);
   draw z3{up}..z4{left}..{down}z5..{dir-112}z6--z7{right}..z8;
endchar;

beginchar("M",.8U#,U#,0); "M"; pickup thick_pen;
   z1=(a,0); z2=(2a,5b); z3=(3.75a,7.5b); z4=(4.3a,4b); z5=(6.75a,7.5b);
   z6=(7a,5b); z7=(6a,0); draw z1--z2{z2-z1}..{right}z3..{down}z4{dir68}%%
   ..{right}z5..{z1-z2}z6--z7;
endchar;

beginchar("N",.7U#,U#,0); "N"; pickup thick_pen;
   z1=(a,0); z2=(2a,2.5b); z3=(3a,7.5b); z4=(4.5a,0); z5=(6a,7.5b);
   draw z1..{z3-z2}z2--z3--z4--z5;
endchar;

beginchar("O",.7U#,U#,0); "O"; pickup thick_pen;
   z1=(6a,5b); z2=(5a,7.5b); z3=(3.5a,7b); z4=(a,3b); z5=(2.5a,0);
   z6=(3.5a,0);
   draw z2{left}..z3..z4..{right}z5--z6{right}..z1..{left}z2;
endchar;

beginchar("P",.7U#,U#,0); "P"; pickup thick_pen;
   z1=(1.5a,0); z3=(3a,7.5b); z2=8/15[z1,z3]; z4=(a,6.5b); z5=(6a,5.5b);
   draw z1--z3; draw z4{dir45}..{right}z3..{z1-z3}z5..{left}z2;
endchar;

beginchar("Q",.7U#,U#,0); "Q"; pickup thick_pen;
   z1=(6a,5b); z2=(5a,7.5b); z3=(3.5a,7b); z4=(a,3b); z5=(2.5a,0);
   z6=(3.5a,0); z7=(3a,2b); z8=(6.4a,-.5b); draw z8{left}..{left}z7;
   draw z2{left}..z3..z4..{right}z5--z6{right}..z1..{left}z2;
endchar;

beginchar("R",.7U#,U#,0); "R"; pickup thick_pen;
   z1=(1.5a,0); z3=(3a,7.5b); z2=8/15[z1,z3]; z4=(a,6.5b); z5=(5a,5.5b);
   z6=(6a,0); draw z2{right}..{right}z6;
   draw z1--z3; draw z4{dir45}..{right}z3..{z1-z3}z5..{left}z2;
endchar;

beginchar("S",.7U#,U#,0); "S"; pickup thick_pen;
   z1=(6a,6.5b); z2=(5a,7.5b); z3=(3.5a,7b); z4=(a,2b); z5=(2a,0);
   z6=(3.5a,0); z7=(6a,2b); z8=(4a,4b);
   draw z1..{left}z2..z3..z8{dir-20}..z7..{left}z6--z5{left}..z4;
endchar;

beginchar("T",.7U#,U#,0); "T"; pickup thick_pen;
   z1=(2.3a,1.5b); z2=(3.5a,7.5b); z3=(a,6b); z4=(6a,7.5b); z5=(4a,0);
   draw z2--z1{z1-z2}..{right}z5; draw z3{dir60}..z2{right}..{dir30}z4;
endchar;

beginchar("U",.7U#,U#,0); "U"; pickup thick_pen;
   z1=(6a,7.5b); z2=(5a,2.5b); z3=(3a,0); z4=(2a,0); z5=(a,2.5b);
   z6=(1.8a,6.5b); z7=(1.5a,7.5b); z8=(a,7b); z9=(6a,0);
   draw z1--z2{z2-z1}..{left}z3--z4{left}..{z1-z2}z5--z6{z1-z2}..{left}z7%%
   ..z8; draw z2{z2-z1}..z9;
endchar;

beginchar("V",.7U#,U#,0); "V"; pickup thick_pen;
   z1=(a,7.5b); z2=(2a,0); z3=(6a,7.5b); 
   draw z1{dir-45}..{down}z2{dir68}..{up}z3;
endchar;

beginchar("W",.9U#,U#,0); "W"; pickup thick_pen;
   z1=(a,7.5b); z2=(1.5a,0); z3=(8a,7.5b); z4=(3.8a,4b); z5=(4.5a,0);
   draw z1{dir-45}..{down}z2{dir68}..{dir45}z4{dir-80}..{down}z5{dir68}%%
   ..{up}z3;
endchar;

beginchar("X",.7U#,U#,0); "X"; pickup thick_pen;
   z1=(a,0); z2=(6a,7.5b); z3=1/2[z1,z2]; z4=(4.5a,0); z5=2[z4,z3];
   z7=(a,6b);
   draw z1{dir71.5}..{dir41.5}z3..{dir71.5}z2; 
   draw z4{up}..{dir120}z3..{up}z5; draw z7{right}..z5;
endchar;

beginchar("Y",.65U#,U#,0); "Y"; pickup thick_pen;
   z1=(2a,0); z2=(a,7.5b); z3=(6a,7.5b); z4=1/2[1/2[z2,z3],z1];
   draw z1--z4; draw z2{z1-z4}..z4; draw z3{z1-z4}..z4;
endchar;

beginchar("Z",.7U#,U#,0); "Z"; pickup thick_pen;
   z1=(2a,6b); z2=(3a,7.5b); z3=(6a,7.5b); z4=(a,0); z5=(4.5a,0); 
   z6=(6a,b); z7=(2a,4.25b); z8=(5a,3.25b); z9=(4.5a,7.1b);
   draw z1..{right}z2..z9..z3--z4--z5{right}..z6; draw z7..{right}z8;
endchar;

beginchar(91,.4U#,U#,0); "sqare bracket on"; pickup thick_pen;
   z1=(2a,0); z2=(a,0); z3=(2a,7b); z4=(3a,7b); draw z1--z2--z3--z4;
endchar;

beginchar(92,.6U#,U#,0); "backslash"; pickup thick_pen;
   draw (4a,0)--(a,7b);
endchar;

beginchar(93,.4U#,U#,0); "sqare bracket off"; pickup thick_pen;
   z1=(a,0); z2=(2a,0); z3=(3a,7b); z4=(2a,7b); draw z1--z2--z3--z4;
endchar;

%%% The numbers from 0 to 9 %%%

beginchar("0",.6U#,U#,0); "zero"; pickup thick_pen;
   z1=(2.5a,0); z2=(4a,0); z3=(5a,2.5b); z4=(3a,4b); z5=(2a,3.5b);
   draw z1--z2{right}..z3..z4..z5..{right}z1..cycle;
endchar;

beginchar("1",.6U#,U#,0); "one"; pickup thick_pen;
   z1=(a,0); z2=(5a,0); z3=(3a,0); z4=(4a,7b); z5=(2a,5b);
   draw z1--z2; draw z3--z4..{left}z5;
endchar;

beginchar("2",.6U#,U#,0); "two"; pickup thick_pen;
   z1=(5a,b); z2=(4.5a,0); z3=(a,0); z4=(5a,4b); z5=(3.5a,7b); 
   z6=(2.5a,6b); draw z1..{left}z2--z3{(1,7)}..z4{dir45}..{left}z5..z6;
endchar;

beginchar("3",.6U#,U#,0); "three"; pickup thick_pen;
   z1=(a,b); z2=(2.5a,0); z3=(4a,0); z4=(5a,2.5b); z5=(3a,4b);
   z6=(3.5a,7b); z7=(2.5a,6b); draw z1..{right}z2--z3{right}..{up}z4%%
   ..{left}z5{dir20}..{left}z6..z7;
endchar;

beginchar("4",.6U#,U#,0); "four"; pickup thick_pen; 
   z1=(4a,0); z2=(5a,7b); z3=(4.5a,4b); z4=(a,4b); z5=(3a,7b);
   draw z1--z2; draw z3--z4{dir45}..z5;
endchar;

beginchar("5",.6U#,U#,0); "five"; pickup thick_pen;  
   z1=(a,b); z2=(2.5a,0); z3=(4a,0); z4=(5a,2.5b); z5=(3a,4b);  
   z6=(2a,3.5b); z7=(2.5a,7b); z8=(5a,7b); draw z1..{right}z2--z3{right}%%
   ..{up}z4..{left}z5..z6--z7--z8
endchar;    

beginchar("6",.6U#,U#,0); "six"; pickup thick_pen;  
   z1=(a,b); z2=(2.5a,0); z3=(4a,0); z4=(5a,2.5b); z5=(3a,4b);  
   z6=(2a,3.5b); z7=(5a,7b); draw z1..{right}z2--z3{right}%%
   ..{up}z4..{left}z5..z6..z1..cycle; draw z6{dir60}..z7;
endchar;

beginchar("7",.6U#,U#,0); "seven"; pickup thick_pen; 
   z1=(a,0); z2=(5.5a,7b); z3=(1.5a,7b); z4=(2a,4b); z5=(4a,4b);
   draw z1..{dir45}z2--z3; draw z4--z5;
endchar;  

beginchar("8",.6U#,U#,0); "eight"; pickup thick_pen;     
   z1=(2.5a,0); z2=(4a,0); z3=(5a,2.5b); z4=(3a,4b); z5=(2a,3.5b);
   draw fullcircle xscaled 3pt yscaled 2pt rotated 40 shifted (3.7a,5.5b);
   draw z1--z2{right}..z3..z4..z5..{right}z1..cycle;
endchar;

beginchar("9",.6U#,U#,0); "nine"; pickup thick_pen;  
   z1=(5.2a,6.2b); z2=(a,0); z3=(2.5a,0); z4=(4.7a,2b);
   draw z1{(-1,-7)}..z4..{left}z3--z2;
   draw fullcircle xscaled 3pt yscaled 2pt rotated 40 shifted (3.7a,5.5b);  
endchar;

beginchar(251,.7U#,U#,0); "AE"; pickup thick_pen;
   z1=(a,0); z2=(2a,5b); z3=(4.3a,6.5b); z4=(6a,5b); z5=(5a,0); z7=(a,4b);
   z6=.4[z4,z5]; z20=(3.5a,7.5b); z21=(6a,7.5b);
   draw z1--z2{z2-z1}..{right}z3..{z1-z2}z4--z5;
   draw z6{left}..{dir135}z7;
   draw fullcircle xscaled .3pt yscaled .6pt rotated -11 shifted z20;
   draw fullcircle xscaled .3pt yscaled .6pt rotated -11 shifted z21;
endchar;

beginchar(252,.7U#,U#,0); "OE"; pickup thick_pen;
   z1=(6a,4.3b); z2=(5a,6.5b); z3=(3.5a,6.05b); z4=(a,2.6b); z5=(2.5a,0);
   z6=(3.5a,0); z20=(3.5a,7.5b); z21=(6a,7.5b);
   draw z2{left}..z3..z4..{right}z5--z6{right}..z1..{left}z2;
   draw fullcircle xscaled .3pt yscaled .6pt rotated -11 shifted z20;
   draw fullcircle xscaled .3pt yscaled .6pt rotated -11 shifted z21;
endchar;

beginchar(253,.7U#,U#,0); "UE"; pickup thick_pen;
   z1=(5.8a,6.5b); z2=(5a,2.5b); z3=(3a,0); z4=(2a,0); z5=(a,2.5b);
   z6=(1.6a,5.5b); z7=(1.3a,6.5b); z8=(a,6b); z9=(6a,0); z20=(2.75a,7.5b); 
   z21=(5.25a,7.5b); draw z1--z2{z2-z1}..{left}z3--z4{left}..{z1-z2}z5--z6%%
   {z1-z2}..{left}z7..z8; draw z2{z2-z1}..z9;
   draw fullcircle xscaled .3pt yscaled .6pt rotated -11 shifted z20;
   draw fullcircle xscaled .3pt yscaled .6pt rotated -11 shifted z21;
endchar;

beginchar(254,U#,U#,0); "pound"; pickup thick_pen;
   z1=(4a,6b); z2=(3a,2.5b); z3=(4a,0); z4=(6a,3b); z5=(7a,6b);
   z6=(6.5a,0); z7=(8a,2b); z8=1/3[z4,z5]; z9=(3a,5b); z10=(a,3b);
   z11=(8a,4.5b);
   draw z1..{down}z2..{right}z3..z4..z5;
   draw z5..z4..{right}z6..{up}z7..z8..{left}z9..{down}z10..{right}z2..z11;
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% end of file chinasym.alf %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
