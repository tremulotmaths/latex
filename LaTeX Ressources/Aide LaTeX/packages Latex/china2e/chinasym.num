%% This file is part of the CHINASYM package, 
%% a PACKAGE for writing CHINESE CALENDAR SYMBOLS 
%% with LaTeX version 2e. 
%% 
%% -- Copyright 1997 by Udo Heyl, Eisenach. All rights reserved. -- 
%% 
%% Copying of this file is authorized only if either 
%% (1) you make absolutely no changes to your copy, including name, or 
%% (2) if you do make changes, you name it something other than 
%%          CHINASYM.ADD,     CHINASYM.ALF,     CHINASYM.ELE,
%%          CHINASYM.NUM,     CHINASYM.SBL,     CHINASYM.STA,
%%          CHINA10.MF   or   CHINA2E.STY !!!
%% This restriction helps ensure that all styles developed for 
%% DANTE e.V. (the users group of german speaking TeX users) 
%% remain identical. 
%% 
%% You are NOT ALLOWED to distribute this file alone. You are NOT 
%% ALLOWED to take money for the distribution or use of either this 
%% file or a changed version, except for some nominal charge for 
%% copying etc. 
%% 
%% Error Reports in case of UNCHANGED versions to 
%% 
%% Udo Heyl
%% Stregdaer Allee 7
%% 99817 Eisenach
%% Federal Republic of Germany 
%% 
%% or 
%% DANTE, Deutschsprachige Anwendervereinigung TeX e.V. 
%% Postfach 10 18 40 
%% 69008 Heidelberg 
%% Federal Republic of Germany 
%% Email: german@dante.de 
%% 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% This file contains the names of the chinese months, %%
%% which are the FIRST, SECOND, THIRD... and so on. -- %%
%% The 12 SIGNs of these NUMBERS and the SIGN of MONTH %%
%% written by Udo Heyl, Eisenach (Vers.1.0) 97/05/15.  %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar(22+o,U#,U#,0); "yi - the 1st (modified)";
   pickup thick_pen;
   z1=(a,4b); z2=(7a,4b); z3=(6.6a,4.3b); draw z1--z2--z3;
endchar;

beginchar(23+o,U#,U#,0); "tching - the 1st";
   pickup thick_pen;
   z1=(a,0); z2=(2.75a,.1b); z3=(4.5a,.1b); z4=(8a,0); z5=(7.6a,0.3b);
   z6=(4.5a,4b); z7=(8a,4b); z71=(7.6a,4.3b); z8=(a,8b);
   z9=(4.5a,7.9b); z10=(8a,8b); z11=(7.6a,8.3b); z12=(2.75a,6b);
   draw z2--z12; draw z3--z9; draw z1--z4--z5;
   draw z6--z7--z71; draw z8--z10--z11;
endchar;

beginchar(24+o,U#,U#,0); "urh - the 2nd";
   pickup thick_pen;
   z1=(a,2b); z2=(8a,2b); z3=(7.6a,2.3b); 
   z4=(a,6b); z5=(7a,6b); z6=(6.6a,6.3b);
   draw z1--z2--z3; draw z4--z5--z6;
endchar;

beginchar(25+o,U#,U#,0); "san - the 3rd";
   pickup thick_pen;
   z1=(a,b); z2=(8a,b); z3=(7.6a,1.3b); 
   z4=(a,7b); z5=(8a,7b); z6=(7.6a,7.3b);
   z7=(a,4b); z8=(7a,4b); z9=(6.6a,4.3b);      
   draw z1--z2--z3; draw z4--z5--z6; draw z7--z8--z9;
endchar;

beginchar(26+o,U#,U#,0); "sze -the 4th";
   pickup thick_pen;
   z1=(a,0); z2=(a,2b); z3=(a,8b); z4=(3a,8b); z5=(a,2.8b); z6=(5a,8b); 
   z7=(5a,4.4b); z8=(7a,4.4b); z9=(8a,8b); z10=(8a,2b); z11=(8a,0);
   z12=(3a,5b); draw z1--z3--z9--z11; draw z5{dir60}..{up}z12..z4; 
   draw z6..z7{down}..{dir60}z8; draw z2--z10;
endchar;

beginchar(27+o,U#,U#,0); "yu - the 5th";
   pickup thick_pen;
   z1=(a,0); z2=(2a,0); z3=(5.5a,0); z4=(8a,0); z5=(7.6a,.3b);
   z6=(5.75a,2b); z7=(4.5a,4b); z9=(1.5a,4b); z10=(2a,8b);
   z11=(3a,8b); z12=(8a,8b); z13=(7.6a,8.3b);
   draw z1--z4--z5; draw z2--z11; draw z10--z12--z13;
   draw z3{(1,8)}..z6..z7{left}..z9;
endchar;

beginchar(28+o,U#,U#,0); "lu - the 6th";
   pickup thick_pen;
   z1=(a,0); z2=(4a,3.6b); z3=(5a,3.6b); z4=(6a,3.1b); z5=(8a,0);
   z6=(a,4.5b); z7=(8a,4.5b); z8=(7.6a,4.8b); z9=(4.5a,4.6b); z10=(4.5a,8b);
   draw z1{dir30}..{dir60}z2; draw z3..{(2,-1)}z4..z5;
   draw z6--z7--z8; draw z9--z10;
endchar;

beginchar(29+o,U#,U#,0); "tsi - the 7th";
   pickup thick_pen;
   z1=(4a,8b); z2=(4a,b); z3=(5a,.3b); z4=(8a,1.3b); z5=(7.7a,2.3b);
   z6=(a,4b); z7=(8a,6.33b); z8=(7.4a,6.5b);
   draw z1..{down}z2..z3{(3,1)}..{(3,1)}z4{dir150}..z5; draw z6--z7--z8;
endchar;

beginchar(30+o,U#,U#,0); "pa - the 8th";
   pickup thick_pen;
   z1=(a,0); z2=(3.5a,7b); z3=(2.5a,7.8b); z4=(5a,8b); z5=(5a,7.8b); 
   z6=(8a,b);
   draw z1{dir40}..z2; draw z4..{down}z5..{dir-30}z6; 
   pickup thin_pen; draw z3--z5;
endchar;

beginchar(31+o,U#,U#,0); "kieu - the 9th";
   pickup thick_pen;
   z1=(3a,0); z2=(4.5a,3b); z3=(4.5a,8b); z4=(a,4b); z5=(8a,4b);
   z6=(7.5a,3b); z7=(8a,b); draw z1..{up}z2..z3;
   draw z4--z5--z6{(-1,-2)}..{right}z7;
endchar;

beginchar(32+o,U#,U#,0); "shi - the 10th";
   pickup thick_pen;
   z1=(4.5a,0); z2=(4.5a,8b); z3=(a,4.5b); z4=(8a,4.5b); z5=(7.6a,4.8b);
   draw z1--z2; draw z3--z4--z5;
endchar;

beginchar(94+o,U#,U#,0); "yue - the MONTH";
   pickup thick_pen;
   z1=(a,0); z2=(2.5a,2b); z3=(3a,4b); z4=(3.1a,5.8b); z5=(3.1a,7.5b);
   z6=(3.1a,8b); z7=(7a,8b); z8=(7a,7.5b); z9=(7a,5.8b); z10=(7a,4b);
   z11=(7a,0); z12=(6a,.8b); z13=(5.5a,.9b);
   draw z1..z2..z3..z4..z5..z6; draw z3..z10; draw z4..z9; draw z7..z11;
   draw z5..z8; draw z11..z12..z13;
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%
%% END OF CHINASYM.NUM %%
%%%%%%%%%%%%%%%%%%%%%%%%%
